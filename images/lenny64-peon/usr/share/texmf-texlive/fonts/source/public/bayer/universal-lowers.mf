%%
%% This is file `lowers.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% universal.dtx  (with options: `lowers')
%% 
%% Copyright (C) 1997 Christian Holm.
%% 
%% This file is NOT the source for universal, because almost all
%% comments have been stripped from it.  It is NOT the preferred
%% form of universal for making modifications to it.
%% 
%% Therefore you can NOT redistribute and/or modify THIS file.
%% You can however redistribute the complete source (universal.dtx
%% and universal.ins) and/or modify it under the terms of the GNU
%% General Public License as published by the Free Software
%% Foundation; either version 2, or (at your option) any later
%% version.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public
%% License along with this program; if not, write to the
%%   Free Software Foundation, Inc.,
%%   675 Mass Ave, Cambridge,
%%   MA 02139, USA.
%% 
%% See the file universal.dtx for further comments and documentation.
%% 
bauhauschar("a",1x_width#,1x_height#,0,0); %"The letter a";
  bot z1 = (7/16wi,0);
  lft z2 = lft z3 rotatedaround((0.5wi,0.5h),45);
  lft z3 = (0,0.5h);
  top z4 = (0.5wi,h);
  rt  z5 = top z4 rotatedaround((0.5wi,0.5h),-45);
  rt  z6 = (wi,0.5h+sind(15));
  rt  z7 = (wi,0);
  draw z6..z1{left}..z2..z3{up}..z4{right}..z5..z6{down}--z7 ;
  penlabels(1,2,3,4,5,6,7,8);
  cutoff(z7,-90);
  a_pic := currentpicture;
endchar;

bauhauschar("b",1x_width#,l_height#,0,0); %"The letter b";
  top y1=5/8h ;
  rt  x2=wi ;
  bot y3=0 ;
  lft x4= lft x5 = lft x6 = 0 ;
  x1 = x3 = 0.5wi ;
  y2 = y4 = y5 = 5/16h ;
  y6 = h ;
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle ;
  cutdraw z5--z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("c",c_width#,1x_height#,0,1f_space#); %"The letter c";
  rt z5 = top z4 rotatedaround((0.5wi,0.5h),-60);
  rt z1 = bot z2 rotatedaround((0.5wi,0.5h),60);
  bot y2=0;
  lft x3=0;
  top y4=h;
  x2 = x4 = 0.5wi;
  y3 = 0.5h ;
  cutdraw z1..z2{left}..z3{up}..z4{right}..z5;
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("d",1x_width#,l_height#,0,0); %"The letter d";
  top y1 = 5/8h ;
  rt  x2 = rt x5 = rt x6 = wi ;
  bot y3 =0 ;
  lft x4 = 0 ;
  x1 = x3 = 0.5wi ;
  y2 = y4 = y5 = 5/16h ;
  y6 = h ;
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle ;
  cutdraw z5--z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("e",1x_width#,1x_height#,0,0) ; %"The letter e";
  rt z1 = bot z2 rotatedaround((0.5wi,0.5h),60) ;
  bot y2 = 0 ;
  lft x3 = 0 ;
  top y4 = h ;
  rt x5 = rt x7 = wi ;
  x6 = p_thick;
  x2 = x4 = 0.5wi ;
  y3 = y5 = y6 = 0.5h ;
  y7 = y5 - 0.5p_thick ;
  cutdraw z5{up}..z4{left}..z3{down}..z2{right}..z1 ;
  cutdraw z5{down}--z7{down} ;
  draw z6{right}--z5{right};
  penlabels(1,2,3,4,5,6,7);
  e_pic := currentpicture
endchar;

bauhauschar("f",f_width#,l_height#,0,0); %"The letter f";
  lft x1 = lft x2 = 0 ;
  y1 = 0 ;
  top y2 = top y5 = top y6 = 5/8h  ;
  x4 = wi ;
  x5 = p_thick;
  x6 = wi;
  top y4 = h ;
  lft z3 = lft z2 rotatedaround((top z6),-45) ;
  cutdraw z1{up}--z2{up}..z3{(1,1)}..z4{right} ;
  cutdraw z5{right}--z6 ;
  penlabels(1,2,3,4,5,6,7);
endchar ;

bauhauschar("g",1x_width#,1x_height#,p_depth#,0); %"The letter g";
  rt  z1  = top z2 rotatedaround((0.5wi,0.5h),-60) ;
  top z2 = (0.5wi,h) ;
  lft z3 = (0,0.5h) ;
  bot z4 = (0.5wi,0) ;
  rt  z5 = (wi,0.5h) ;
  rt  z6 = (wi,-d)    ;
  cutdraw z1{(-cosd(60),sind(60))}..z2{left}..z3{down}
    ..z4{right}..z5{up} ;
  cutdraw z5{down}--z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("h",1x_width#,1l_height#,0,0) ; %"The letter h" ;
  lft z1 = (0,0) ;
  lft z2 = (0,h) ;
  rt  z3 = (wi,0) ;
  rt  z4 = (wi,x5) ;
  top z5 = (0.5wi,5/8h) ;
  lft z6 = (0,x5) ;
  cutdraw z1{up}--z2 ;
  cutdraw z3{up}--z4..z5{left}..z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("i",1/5x_height#+f_space#,l_height#,0,0); %"The letter i" ;
  lft z1 = (0,0) ;
  lft z2 = (0,5/8h) ;
  top z3 = (0.5wi,h) ;
  cutdraw z1{up}--z2 ;
  drawdot z3 ;
  penlabels(1,2,3);
endchar;

bauhauschar("j",1/5x_height#+f_space#,l_height#,p_depth#,0);
  %"The letter j"
  lft z1 = (0,-d) ;
  lft z2 = (0,5/8h) ;
  top z3 = (0.5wi,h) ;
  cutdraw z1{up}--z2 ;
  drawdot z3 ;
  penlabels(1,2,3);
endchar;

bauhauschar("k",0.8x_width#,1x_height#,0,0); %"The letter k" ;
  lft z1 = (0,0) ;
  lft z2 = (0,h) ;
  lft z3 = (0,0.5h) ;
  rt  z4 = (wi,0) ;
  rt  z5 = (wi,h) ;
  cutdraw z1{up}--z2 ;
  cutdraw z3{right}..z4{down} ;
  cutdraw z3{right}..z5{up} ;
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("l",1/5x_height#+f_space#,l_height#,0,0);%"The letter l" ;
  lft z1 = (0,0) ;
  lft z2 = (0,h) ;
  cutdraw z1{up}--z2 ;
  penlabels(1,2,3);
endchar;

bauhauschar("m",1m_width#,1x_height#,0,0); %"The letter m";
  lft z1 = (0,0) ;
  lft z2 = (0,0.7h) ;
  top z3 = (0.5[x5,x1],h) ;
      z4 = (0.5wi,0.7h-0.25p_thick) ;
      z5 = (0.5wi,0) ;
  top z6 = (0.5[x8,x5],h) ;
  rt  z7 = (wi,0.7h) ;
  rt  z8 = (wi,0) ;
  cutdraw z1{up}--z2..z3{right}..z4{down}--z5 ;
  cutdraw z4{up}..z6{right}..z7--z8{down} ;
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhauschar("n",1x_width#,1x_height#,0,0); %"The letter n";
  lft z1 = (0,0);
  lft z2 = (0,0.6h) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,0.6h) ;
  rt  z5 = (wi,0);
  cutdraw z1{up}--z2{up}..z3{right}..z4{down}--z5{down};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("o",1x_width#,1x_height#,0,0); %"The letter o";
  top y1=h ;
  rt  x2=wi ;
  bot y3=0 ;
  lft x4=0 ;
  x1 = x3 = 0.5wi ;
  y2 = y4 = 0.5h ;
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle ;
  penlabels(1,2,3,4);
  o_pic := currentpicture;
endchar ;

bauhauschar("p",1x_width#,1x_height#,p_depth#,0); %"The letter p";
  lft z1 = (0,-d) ;
  lft z2 = (0,0.5h) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,0.5h) ;
  bot z5 = (0.5wi,0) ;
  draw z2{up}..z3{right}..z4{down}..z5{left}..cycle ;
  cutdraw z1{up}--z2{up};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("q",1x_width#,1x_height#,p_depth#,0); %"The letter q";
  rt  z1 = (wi,-d) ;
  lft z2 = (0,0.5h) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,0.5h) ;
  bot z5 = (0.5wi,0) ;
  draw z2{up}..z3{right}..z4{down}..z5{left}..cycle ;
  cutdraw z1{up}--z4{up};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("r",3/5x_height#+f_space#,1x_height#,0,0); %"The letter r";
  lft z1 = (0,0) ;
  lft z2 = (0,h-wi);
  top z4 = (wi,h) ;
  lft z3 = lft z2 rotatedaround((x4,y2),-45);
  cutdraw z1{up}--z2{up}..z3{(1,1)}..z4{right};
  penlabels(1,2,3,4);
endchar;

bauhauschar("s",0.8x_width#,1x_height#,0,0); %"The letter s";
  lft z1  = (0,0.5[y6,y2]) ;
  bot z2  = (1/3wi,0) ;
  bot z3  = (2/3wi,0) ;
  rt  z4  = (wi,0.5[y5,y3]) ;
      z5  = (2/3wi,0.5h) ;
      z6  = (1/3wi,0.5h) ;
  lft z7  = (0,0.5[y8,y6]) ;
  top z8  = (1/3wi,h);
  top z9  = (2/3wi,h) ;
  rt  z10 = (wi,0.5[y8,y6]) ;
  cutdraw z1{down}..z2{right}--z3{right}..z4{up}..
    z5{left}--z6{left}..z7{up}..z8{right}--z9{right}..z10{down} ;
  penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

bauhauschar("t",3/5x_height#+f_space#,1l_height#,0,0); %"The letter t";
  z1 = (0.5wi,0);
  z2 = (0.5wi,h);
  cutdraw z1{up}--z2;
  top z3 = (0,5/8h);
  top z4 = (wi,5/8h);
  cutdraw z3--z4;
  penlabels(1,2,3,4);
endchar;

bauhauschar("u",1x_width#,1x_height#,0,0); %"The letter u";
  lft z1 = (0,h);
  lft z2 = (0,0.4h) ;
  bot z3 = (0.5wi,0) ;
  rt  z4 = (wi,0.4h) ;
  rt  z5 = (wi,h);
  cutdraw z1{down}--z2{down}..z3{right}..z4{up}--z5{up};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("v",1x_width#,1x_height#,0,0); %"The letter v";
  lft z1 = (0,h);
  bot z2 = (0.5wi-0.5p_thick,0.5p_thick);
  bot z3 = (0.5wi,0);
  bot z4 = (0.5wi+0.5p_thick,0.5p_thick);
  rt  z5 = (wi,h);
  draw z1{down}--z2..z3..z4--z5{up};
  cutoff(z1,90);
  cutoff(z5,90);
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("w",1m_width#,1x_height#,0,0); %"The letter w";
  lft z1 = (0,h) ;
  lft z2 = (0,0.3h) ;
  bot z3 = (0.5[x5,x1],0) ;
      z4 = (0.5wi,0.3h+0.25p_thick) ;
      z5 = (0.5wi,h) ;
  bot z6 = (0.5[x8,x5],0) ;
  rt  z7 = (wi,0.3h) ;
  rt  z8 = (wi,h) ;
  cutdraw z1{down}--z2..z3{right}..z4{up}--z5 ;
  cutdraw z4{down}..z6{right}..z7--z8{down} ;
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhauschar("x",1x_width#,1x_height#,0,0); %"The letter x";
  top z1 = (0,h);
      z2 = (0.5wi,0.5wi);
  bot z3 = (0,0);
  top z4 = (wi,h);
  bot z5 = (wi,0);
  cutdraw z1{right}..z2{down}..z3{left};
  cutdraw z4{left}..z2{down}..z5{right};
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("y",1x_width#,1x_height#,p_depth#,0) ; %"The letter y" ;
  rt  z1 = (wi,-d) ;
  lft z2 = (0,h) ;
  rt  z3 = (wi,h) ;
  rt  z4 = (wi,x5) ;
  bot z5 = (0.5wi,0) ;
  lft z6 = (0,x5) ;
  cutdraw z1{up}--z3 ;
  cutdraw z3{up}--z4..z5{left}..z6{up}--z2{up} ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("z",1x_width#,1x_height#,0,0) ; %"The letter z" ;
  top z1 = (0,h);
  rt  x2 = wi         ; top y2 = h ;
  rt  x3 = wi         ; top y3 = y2;
  lft x4 = 0         ; bot y4 = y5 ;
  lft x5 = 0         ; bot y5 = 0 ;
  bot z6 = (wi,0);
  cutdraw z1{right}--z2..z3--z4..z5--z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;


\endinput
%%
%% End of file `lowers.mf'.
