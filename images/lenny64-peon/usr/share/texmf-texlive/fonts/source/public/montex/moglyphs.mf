%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%        File: moglyphs.mf
%      Author: Oliver Corff
%        Date: June 1st, 2001
%     Version: 0.2
%   Copyright: Ulaanbaatar, Beijing, Berlin
%
% Description: Local Mongolian Script (LMS)
%              Mongol Bicig Glyph Definitions
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
	writing:=Bicig;
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_TASLAL,tewidth#,height#,depth#);
	"Taslal temdeg";
	dusal(2/4tewidth,Centerline,5thin,3thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_FULLSTOP,bawidth#,height#,depth#);
	"Mongolian Full Stop";
	dusal(1/4bawidth,Centerline,5thin,3thick);
	dusal(3/4bawidth,Centerline,5thin,3thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_AVAR,shudwidth#,height#,depth#);
	"Mongol letter a, final behaviour different";
	shud(0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_AE,shudwidth#,height#,depth#);
	"Mongol letter ae, shape identical with shud";
	shud(0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_IVAR,shilbewidth#,height#,depth#);
	"Mongol letter i, final behaviour different";
	shilbe(0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_NVAR,shudwidth#,height#,depth#);
	"Mongol letter n, but with dusal behaviour flipped";
	shud(0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ADAGN,shudwidth#,height#,depth#);
	"Mongol letter n, final form";
	"The suul";
	suul;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ADAGNVAR,shudwidth#,height#,depth#);
	"Mongol letter n, final form, but with dusal behaviour flipped";
	"The suul";
	suul;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary shud after B, etc.";
if unknown MONG_SECONDARYSHUD:
	"skipped";
else:
beginchar(MONG_SECONDARYSHUD,numtaigedeswidth#,height#,depth#);
	nuruu(secAwidth,0);
	shud_atom(secAwidth,bearing);

	z5r=(numtaigedeswidth,underouter);
	z6l=(secAwidth,nuruuright);

	penpos5(underinner-underouter,-90);
	penpos6(2thin,-180);
	penstroke (z5e{left}..{up}z6e) ifrotated;
	penlabels(5,6);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary Belly after B, etc.";
if unknown MONG_SECONDARYGEDES:
	"skipped";
else:
beginchar(MONG_SECONDARYGEDES,numtaigedeswidth#,height#,depth#);
	nuruu(numtaigedeswidth,0);
	z1r=(numtaigedeswidth,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(1/2bearing,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;
	z5r=(x1r,underouter);
	z6 =(x3,nuruuright);
	penpos5(underinner-underouter,-90);
	penpos6(thin,-180);
	penstroke (z5e{left}..{up}z6e) ifrotated;
	penlabels(1,2,3,5,6);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary dusaltai shud after B, etc.";
if unknown MONG_SECONDARYCEGTEISHUD:
	"skipped";
else:
beginchar(MONG_SECONDARYCEGTEISHUD,numtaigedeswidth#,height#,depth#);
	"Mongol Secondary dusaltai shud after B, etc.";
	nuruu(secAwidth,0);
	shud_atom(secAwidth,bearing);

	z5r=(numtaigedeswidth,underouter);
	z6l=(secAwidth,nuruuright);

	penpos5(underinner-underouter,-90);
	penpos6(2thin,-180);
	penstroke (z5e{left}..{up}z6e) ifrotated;
	dusal (1/2secAwidth+1/2bearing,yashilbeheight,3thin,2thick);
	penlabels(5,6);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_NUM,gedeswidth#,height#,depth#);
	"Mongol Female g";
	nuruu(gedeswidth,gedeswidth-bearing);
	z1l=(1/3gedeswidth,shudheight);
	z2r=(gedeswidth-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6r=(0,Centerline);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(1/2thin,-180);
	
	penstroke (z1e..z2e...z4e{down}..z5e..{up}z6e) ifrotated;
	penlabels(1,2,3,4,5,6);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol female g, before rounded vowels";
if unknown MONG_SECONDARYNUM:
	"skipped";
else:
beginchar(MONG_SECONDARYNUM,shudwidth#,height#,depth#);
	nuruu(shudwidth,shudwidth-bearing);
	z1r=(+3thin,shudheight);
	z2r=(shudwidth-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(0,underouter);
	z6r=(0,nuruuleft);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(epsilon,-180);
	
	penstroke (z1e..z2e...z4e{down}..{left}z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_DAWXARCEGTXEWTEE,tewidth#,height#,depth#);
	"Mongol Letter initial g(amma)";
	xewteeqix;
	dusal (x2,shilbeheight,3thin,2thick);
	dusal (bearing+2thin,shilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ADAGG,2shudwidth#,height#,depth#);
	"Final g";
	shud(shudwidth);
	bagaodoisuul;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_DVAR,eswidth#,height#,depth#);
	"d FVS1";
	if style=steel:
		roundgedestshilbe;
	fi;
	if style=wood:
		modgedestshilbe;
	fi;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_SECERWEELJIN,gedeswidth#,height#,depth#);
	"Mongol Letter 'r' in final position";
	nuruu(gedeswidth,gedeswidth-bearing);
	z1=(gedeswidth-1/2bearing,nuruuleft);
	z2=(gedeswidth-3thin,gedesouter);
	z3=(bearing,shudheight);
	z4=(x2-1thin,gedesinner-thin);
	z5 =(x6l,nuruuleft);
	pickup pencircle scaled 1/4thin;

	z6r=(x1,nuruuright);
	z7r=(1/2[x6,x8],underouter);
	z8r=(0,Centerline);
	penpos6(2thin,0);
	penpos7(underinner-underouter,-90);
	penpos8(1/4thin,-180);

	fill (z1{up}..z2 & z2{dir 225}..{dir 105}z3 &
	 	z3{down}..{dir 45}z4 & z4{dir -60}..z5..z6l{down}..z7l..{up}z8 &
			z8{down}..z7r..{up}z6r..z1..cycle) ifrotated;
	penlabels(1,2,3,4,5,6,7,8);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_EXENSH,tewidth#,height#,depth#);
	"Mongol Letter `sh' in initial position";
	exenzawj;
	dusal(bearing,underouter,3thin,2thick);
	dusal(1/3tewidth,underouter,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_DAWXARCEGTZAWJ,tewidth#,height#,depth#);
	"Mongol Letter `sh' in medial position";
	zawj;
	dusal(bearing,underouter,3thin,2thick);
	dusal(1/3tewidth,underouter,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ADAGSH,tewidth#,height#,depth#);
	"Mongol Letter 'sh' in final position ";
	nuruu(tewidth,tewidth-thick);
	z1=(tewidth-bearing,nuruuleft);
	z2=(1/2[x1,x3],shudheight);
	z3=(x1-shudwidth+2bearing,y1);
	y4=nuruuright-1thin;
	z4=whatever[z2,z3];

	y5=shudheight;
	z4-z5=whatever * (z2-z1);
	% y6=nuruuleft;
	% z5-z6=whatever * (z2-z3);
	z6=(0,underouter);

	y7=sdepth;
	z4-z7=whatever * (z1-z2);
	y8=nuruuright;
	z7-z8=whatever * (z2-z3);
	y9=underouter;
	z7-z9=whatever * (z7-z8);
	y10=nuruuright;
	z10-z9=whatever* (z1-z2);
	z11=(x6,nuruuright);

	% pickup pencircle scaled 1/4thin;
	fill (z10--z9--z1--z2--z4--z5..z6{z3-z2}&z6{z2-z6}..z10--cycle)
		ifrotated;
	dusal(4/24tewidth,underouter-2thin,3thin,2thick);
	dusal(10/24tewidth,underouter-2thin,3thin,2thick);
	penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Letter Secondary B";
if unknown MONG_SECNUMTGEDES:
	"skipped";
else:
beginchar(MONG_SECNUMTGEDES,numtaigedeswidth#,height#,depth#);
	nuruu(numtaigedeswidth,0);
	z1r=(numtaigedeswidth-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(0,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(x3r,underouter);
	z6r=(0,nuruuright);
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(epsilon,-180);
	penstroke (z4e{down}..{left}z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary Letter P";
if unknown MONG_SECNUMTDELBENQ:
	"skipped";
else:
beginchar(MONG_SECNUMTDELBENQ,numtaigedeswidth#,height#,depth#);
	nuruu(numtaigedeswidth,0);

	z1r=(numtaigedeswidth-1/2bearing,gedesouter+thin);
	z2r=(1/3[x1,x4],gedesouter);
	z3r=(7/12[x1,x4],gedesouter);
	z4r=(0,nuruuleft);
	penpos1(thin,180);
	penpos2(gedesouter-gedesinner,90);
	penpos3(gedesouter-gedesinner,90);
	penpos4(thin,180);
	penstroke (z1e..z2e..z3e..{down}z4e) ifrotated;

	z5r=(x1,nuruuright);
	z6r=(x4r,underouter);
	z7r=(0,nuruuright);
	penpos5(thin,0);
	penpos6(underinner-underouter,-90);
	penpos7(epsilon,-180);
	penstroke (z5e{down}..{left}z6e) ifrotated;
	penlabels(1,2,3,4,5,6,7);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_SERTENQIXTNUM,eswidth#,height#,depth#);
	"Mongol Letter f";
	nuruu(eswidth,eswidth-numtaigedeswidth);

	z1r=(eswidth-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(eswidth-numtaigedeswidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6r=(0,Centerline);
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(1/4thin,-180);
	penstroke (z4e{down}..z5e..{up}z6e) ifrotated;

	f_flag(eswidth,x2);
	
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary Letter F";
if unknown MONG_SECSERTENQIXT:
	"skipped";
else:
beginchar(MONG_SECSERTENQIXT,numtaigedeswidth#,height#,depth#);
	nuruu(numtaigedeswidth,0);
	z1r=(numtaigedeswidth-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(0,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(x3r,underouter);
	z6r=(0,nuruuright);
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(epsilon,-180);
	penstroke (z4e{down}..{left}z5e) ifrotated;

	f_flag(numtaigedeswidth,x2);

	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_QAGT,gedeswidth#,height#,depth#);
	"Mongol Letter kh";
	qagt;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol Secondary Letter K";
if unknown MONG_SECONDARYQAGT:
	"skipped";
else:
beginchar(MONG_SECONDARYQAGT,shudwidth#,height#,depth#);
	nuruu(shudwidth,shudwidth-bearing);
%	if writing=Bithe:
%		z1 =(w+1/4shudwidth,shudheight-thin);
%		penpos1(2.5thin,-30);		
%	fi;
%	if writing=Bicig:
		z1l=(1/3shudwidth,shudheight);
		penpos1(4thin,60);
%	fi;
	z2r=(shudwidth-1/2bearing,nuruuleft);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(0,underouter);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	
	penstroke (z1e..z2e...z4e{down}..{left}z5e) ifrotated;
	penlabels(1,2,3,4,5);
	z11=(shudwidth,Centerline);
	z12=(shudwidth-3/4thick,Centerline);
	z13=(shudwidth-1.5thick,nuruuleft+thin);
	penpos11(nuruuwidth,90);
	penpos12(14/16nuruuwidth,80);
	penpos13(epsilon,45);
	penstroke (z11e---z12e..z13e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,11,12,13);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ZADGAIZARDIGT,cewidth#,height#,depth#);
	"Mongol Letter Z";
	nuruu(cewidth,0);
	z1r=(5/6cewidth,gedesouter);
	z2r=(4/6cewidth,shudheight);
	z3r=(5/4bearing+1.5u,shudheight);
	z4 =(bearing+1.5u,Centerline);
	penpos1(gedesouter-gedesinner,85);
	penpos2(gedesouter-gedesinner,85);
	penpos3(gedesouter-gedesinner,100);
	penpos4(nuruuleft-nuruuright,100);
	penstroke (z1e..z2e---z3e--z4e) ifrotated;
	penlabels(1,2,3,4);
	z5l=z3r;			penpos5(thin,0);
	z6=(-0.5thin,shilbeheight);	penpos6(1.25thin,90);
	z7=(-1.5thin,shilbeheight-thin);	penpos7(0.5thin,120);
	pickup pencircle scaled thin;
	penstroke (z5e{up}..z6e..z7e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_BITUUZARDIGT,cewidth#,height#,depth#);
	"Mongol Letter C";
	nuruu(cewidth,0);
	z1r=(5/6cewidth,gedesouter);
	z2r=(4/6cewidth,shudheight);
	z3r=(5/4bearing+1.5u,shudheight);
	z4 =(bearing+1.5u,Centerline);
	penpos1(gedesouter-gedesinner,85);
	penpos2(gedesouter-gedesinner,85);
	penpos3(gedesouter-gedesinner,100);
	penpos4(nuruuleft-nuruuright,100);
	penstroke (z1e..z2e---z3e--z4e) ifrotated;
	penlabels(1,2,3,4);
	
	z5l=z3r;			penpos5(thin,0);
	z6=(-0.5thin,shilbeheight);	penpos6(1.25thin,90);
	pickup pencircle scaled thin;
	draw (z5..tension 1.3..z6..tension 1.3..cycle) ifrotated;
	z7=(-2thin,shilbeheight-thin);	penpos7(0.5thin,120);
	% penstroke (z5e{up}..z6e..z7e);
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_BITUUSHILBE,eswidth#,height#,depth#);
	"Mongol Letter h";
	nuruu(eswidth,eswidth-1/2bearing-2thin);
	nuruu(eswidth-1/2bearing-2u,0);
	z1r=(eswidth-1/2bearing,nuruuleft);
	z2 =(07/16eswidth,orxicouter);
	penpos1(2thin,0);
	penpos2(2thin,45);
	penstroke (z1e--z2e) ifrotated;
	z3r=z2;
	z4r=(bearing,5/12[y1,y2]);

	penpos3(2thin,115 );
	penpos4(2thin,210);
	
	z5 =(6/16eswidth,3/12[y1,y2]);
	z6 =(x1-2u,nuruuleft);

	penpos5(1.5thin,235);
	penpos6(1.75thin,180);
	penstroke
		(z3e{dir 205}..tension 1.4..z4e..tension 1.4..z5e..z6e) 
			ifrotated;
	penlabels(1,2,3,4,5,6);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_MALGAITAITITEM,tewidth#,height#,depth#);
	"Mongol Letter j";
	if style=wood:
		nuruu(7/12tewidth,0);
		shud_atom(shudwidth-bearing,0+bearing);
	fi;
	if style=steel: titem; fi;

	z1l=(2/3shudwidth-thin,shilbeheight);
	z2l=(2/3shudwidth+thin,shilbeheight);
	z3r=(tewidth,Centerline);
	z4l=(1/2[x3,x5],
		if style=steel: underouter fi
		if style=wood:  underinner fi);
	z5l=(	if style=steel: 1/3 fi
		if style=wood:	2/3 fi shudwidth,nuruuright);
	penpos1(3thin,90);
	penpos2(2.25thin,90);
	penpos3(2.25thin, 0);
	penpos4(3thin,-90);
	penpos5(2thin,-180);
	penstroke (z1e{right}..z2e{right}..z3e..z4e..{up}z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_EWERTQAGT,gedeswidth#,height#,depth#);
	"Mongol g used in Gombo, primary version";
	nuruu(gedeswidth,gedeswidth-bearing);
	z1l=(1/3gedeswidth,shudheight);
	z2r=(gedeswidth-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6r=(0,Centerline);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(1/2thin,-180);
	
	z7r=(gedeswidth-bearing,gedesouter+thin);
	z8 =(x7-1thin,shudheight-thick);
	penpos7(1.5thin,0);
	penpos8(2thin,-45);
	penstroke (z7e..z8e) ifrotated;

	penstroke (z1e..z2e...z4e{down}..z5e..{up}z6e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	"Mongol g used in Gombo, secondary version";
if unknown MONG_SECONDARYEWERTQAGT:
	"skipped";
else:
beginchar(MONG_SECONDARYEWERTQAGT,shudwidth#,height#,depth#);
	nuruu(shudwidth,shudwidth-bearing);
	z1l=(+1thin,shudheight);
	z2r=(shudwidth-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(0,underouter);
	z6r=(0,nuruuleft);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(epsilon,-180);
	
	z7r=(shudwidth,gedesouter+thin);
	z8 =(x7-1thin,shudheight-thick);
	penpos7(1.5thin,0);
	penpos8(2thin,-45);
	penstroke (z7e..z8e) ifrotated;

	penstroke (z1e..z2e...z4e{down}..{left}z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
fi;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Some ligatures without added semantical value
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGPN,bawidth#,height#,depth#);
	"Mongol Ligature PN";
	ligmongpa;
	dusal (x8,yashilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGPL,bawidth#,height#,depth#);
	"Mongol Ligature PL";
	ligmongpa;
	high_l_ewer(2.5thin);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGFA,bawidth#,height#,depth#);
	"Mongol Ligature FA";
	ligba;
	f_flag(w,3/4w);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGFI,biwidth#,height#,depth#);
	"Mongol Ligature FI";
	nuruu(shilbewidth-thin,0);
	nuruu(biwidth,biwidth-numtaigedeswidth);

	z1r=(biwidth-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(biwidth-numtaigedeswidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6l=(shilbewidth,nuruuright);
	z7l=(3thin,shilbeheight);
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(2thin,-180);
	penpos7(4thin,-120);
	penstroke (z4e{down}..z5e..{up}z6e..tension infinity..z7e) ifrotated;

	z11l=(x2,gedesouter);
	penpos11(1/2thin,90);
	z12 =(1/6[x11,x14],shilbeheight);
	penpos12(2thin,90);
	z13 =(5/6[x11,x14],shilbeheight);
	penpos13(2thin,90);
	z14r=(biwidth,yashilbeheight);
	penpos14(1/2thin,90);
	penstroke (z11e{up}..z12e..z13e..{up}z14e) ifrotated;
	f_flag(w,3/4w);
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGFU,2numtaigedeswidth#,height#,depth#);
	"Mongol Ligature FU";
	f_flag(w,3/4w);
	ligbu(0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGFN,bawidth#,height#,depth#);
	"Mongol Ligature FN";
	ligba;
	f_flag(w,3/4w);
	dusal (x7,yashilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGFL,bawidth#,height#,depth#);
	"Mongol Ligature FL";
	ligba;
	f_flag(w,3/4w);
	high_l_ewer(2.5thin);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKN,bawidth#,height#,depth#);
	"Mongol Ligature KN";
	ligka;
	dusal (x7,yashilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKL,bawidth#,height#,depth#);
	"Mongol Ligature KL";
	ligka;
	high_l_ewer(2.5thin);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKHA,bawidth#,height#,depth#);
	"Mongol Ligature KHA";
	ligkha;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKHI,biwidth#,height#,depth#);
	"Mongol Ligature KHI";
	ligkhi;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKHU,bawidth#,height#,depth#);
	"Mongol Ligature KHU";
	ligkhu;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKHN,bawidth#,height#,depth#);
	"Mongol Ligature KHN";
	ligkha;
	dusal (x7,yashilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGKHL,bawidth#,height#,depth#);
	"Mongol Ligature KHL";
	ligkha;
	high_l_ewer(2.5thin);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGGHA,bawidth#,height#,depth#);
	"Mongol Ligature GHA";
	liggha;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGGHI,biwidth#,height#,depth#);
	"Mongol Ligature GHI";
	nuruu(shilbewidth-thin,0);
	nuruu(biwidth,biwidth-bearing);

	z1l=(biwidth-gedeswidth+5thin,shudheight-thin);
	z2r=(biwidth-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z4 =(x2,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6l=(shilbewidth,nuruuright);
	z7l=(3thin,shilbeheight);
	penpos4(2thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(2thin,-180);
	penpos7(4thin,-120);

	penstroke (z1e..z2e...z4e{down}..z5e..{up}z6e..tension
		infinity..z7e) ifrotated;

	z10r=(biwidth,gedesouter+thin);
	z11 =(x10-2thin,shudheight-5thin);
	penpos10(1.5thin,0);
	penpos11(2thin,-45);
	penstroke (z10e..z11e) ifrotated;

	penlabels(1,2,3,4,5,6,7,8,9);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGGHU,shudwidth#+numtaigedeswidth#,height#,depth#);
	"Mongol Ligature GHU";
	nuruu(w,w-bearing);
	nuruu(numtaigedeswidth,0);
	z1l=(w-shudwidth+1thin,shudheight);
	z2r=(w-1/2bearing,nuruuleft);
	penpos1(4thin,60);
	penpos2(2thin,0);

	z3 =(x2,nuruuright);
	z4r=(numtaigedeswidth,underouter);
	z5r=(1/2bearing,nuruuright);
	penpos3(2thin,0);
	penpos4(underinner-underouter,-90);
	penpos5(thin,-180);
	
	z6 =(x5,nuruuleft);
	z7r=(1/2[x6,x8],gedesouter);
	z8r=(numtaigedeswidth,y6);
	penpos6(thin,-180);
	penpos7(gedesinner-gedesouter,-90);
	penpos8(thin,0);
	

	z9r=(w,gedesouter+thin);
	z10=(x9-1thin,shudheight-thick);
	penpos9(1.5thin,0);
	penpos10(2thin,-45);
	penstroke (z9e..z10e) ifrotated;

	penstroke (z1e..z2e...z3e{down}..z4e..{up}z5e) ifrotated;
	penstroke (z6e{up}..z7e..{down}z8e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGGHN,bawidth#,height#,depth#);
	"Mongol Ligature GHN";
	liggha;
	dusal (x7,yashilbeheight,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_LIGGHL,bawidth#,height#,depth#);
	"Mongol Ligature GHL";
	liggha;
	high_l_ewer(2.5thin);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% vim: ts=8
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
