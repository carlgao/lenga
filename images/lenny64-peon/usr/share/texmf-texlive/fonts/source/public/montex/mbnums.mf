%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%        File: mbglyphs.mf
%      Author: Oliver Corff and Dorjpalam Dorj
%        Date: June 1st, 1999
%     Version: 0.2
%   Copyright: Ulaanbaatar, Beijing, Berlin
%
% Description: Local Mongolian Script (LMS)
%              Mongol Bicig Modyn Bar Glyph Definition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
show param_base;
beginchar(MONG_ZERO,numberwidth#+bearing#,height#,depth#);
	"The Number 0";
	pair dz[];
	numberpath;
	z11r= point 0.20 of trial;
	dz11= direction 0.20 of trial;
	penpos11(2thin,angle dz11+90);

	z12r= point 0.75 of trial;
	dz12= direction 0.75 of trial;
	penpos12(thin,angle dz12+90);

	z13r= point 2.20 of trial;
	dz13= direction 2.20 of trial;
	penpos13(2thin,angle dz13+90);

	z14r= point 2.8 of trial;
	dz14= direction 2.8 of trial;
	penpos14(1thin, angle dz14+90);

	penstroke (z11e{dz11}..{dz12}z12e..
		z13e{dz13}..z14e{dz14}..cycle) ifrotated;

	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_ONE,numberwidth#+bearing#,height#,depth#);
	"The Number 1";
	pair dz[];
	numberpath;

	z11r= point 0.20 of trial;
	dz11= direction 0.20 of trial;
	penpos11(4thin,angle dz11+90);

	z12r= point 0.75 of trial;
	penpos12(thin,0);

	z13r= point 2.00 of trial;
	dz13= direction 2.00 of trial;
	penpos13(2thin,angle dz13+90);

	z14r= point 2.8 of trial;
	dz14= direction 2.8 of trial;
	penpos14(2thin, angle dz14+90);

	z15r= point 4.00 of trial;
	dz15= direction 4.00 of trial;
	penpos15(1/2thin, angle dz15+90);
	
	pickup pencircle scaled 4 thin;
	drawdot (z11) ifrotated;

	penstroke (z11e{dz11}..z12e{down}..{dz13}z13e
		..{dz14}z14e..{dz15}z15e) ifrotated;

	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_TWO,numberwidth#+bearing#,height#,depth#);
	"The Number 2";
	pair dz[];
	numberpath;	
	z11=(2bearing,gedesinner);
	penpos11(3thin,+90);

	z12l= point 0.75 of trial;
	dz12= direction 0.75 of trial;
	penpos12(1thin,angle dz12+90);

	z13r= point 1.80 of trial;
	dz13= direction 1.80 of trial;
	penpos13(2thin,angle dz13+90);

	z14l= z16l;
	% z14l= (bearing,Centerline);
	dz14= direction 0.75 of trial;
	penpos14(2thin,  -90);

	z15r= (bearing,Centerline+4thin);
	dz15= direction 0.75 of trial;
	penpos15(1/2thin,  -180);

	z16r= (bearing,Centerline+2thin);
	dz16= direction 0.75 of trial;
	penpos16(2thin,  -180);

	z17r= (bearing,underinner+thin);
	penpos17(2thin,  180);

	z18l= (bearing+1/12bawidth,underinner);
	penpos18(1/2thin,180);

	pickup pencircle scaled 3thin;
	drawdot (z11) ifrotated;
	penstroke (z11e{right}..z12e{dz12}..z13e{dz13}..
		tension 1.2..z14e) ifrotated;
	penstroke (z15e--z16e--z17e--z18e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_THREE,numberwidth#+bearing#,height#,depth#);
	"The Number 3";
	pair dz[];
	numberpath;	

	z9 =(thin+bearing,gedesinner);
	dz9= direction -0.10 of trial;
	penpos9(0.75thin,angle dz9+90);

	z10r=(numberwidth-bearing,Centerline-thin);
	dz10= direction 0.90 of trial;
	penpos10(3thin,angle dz10+90);
	
	z11=(1/3numberwidth,nuruuleft+thin);
	dz11= direction 2.40 of trial;
	penpos11(1/2thin, angle dz11+90);

	z12r= (5/6numberwidth,nuruuright+thin);
	%dz12= direction 0.75 of trial;
	penpos12(2thin,30);

	z13r= point 1.60 of trial;
	dz13= direction 1.60 of trial;
	penpos13(3thin,angle dz13+90);

	z14r= (2thin+bearing,Centerline);
	dz14= direction 0.75 of trial;
	penpos14(2thin,  -90);

	z15l= (bearing,Centerline+4thin);
	dz15= direction 0.75 of trial;
	penpos15(1/2thin,  0);

	z16l= (bearing,Centerline+2thin);
	dz16= direction 0.75 of trial;
	penpos16(2thin,  0);

	z17l= (bearing,underinner+thin);
	penpos17(2thin,  0);

	z18l= (bearing+1/12numberwidth,underinner);
	penpos18(1/2thin,0);

	penstroke (z9e{dz9}..tension 1.4..z10e..tension 1.2..{dz11}z11e)
		ifrotated;
	penstroke (z12e..z13e{dz13}..tension 1.2..z14e) ifrotated;
	penstroke (z15e--z16e--z17e--z18e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_FOUR,numberwidth#+bearing#,height#,depth#);
	"The Number 4";
	pair dz[];
	numberpath;
	z11=(bawidth,underinner);
	penpos11(3thin,-90);

	z12r= point 2.80 of trial;
	dz12= direction 2.80 of trial;
	penpos12(2thin,angle dz12+90);

	z13r= point 0.10 of trial;
	dz13= direction 0.10 of trial;
	penpos13(1thin,angle dz13+90);

	z14r= point 0.75 of trial;
	dz14= direction 0.75 of trial;
	penpos14(1thin, angle dz14+90);

	pickup pencircle scaled 3thin;
	drawdot (z11) ifrotated;
	penstroke (z11e{left}..z12e{dz12}..z13e{dz13}..z14e{dz14}) ifrotated;
	pickup pencircle scaled thin;
	drawdot (z14) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_FIVE,numberwidth#+bearing#,height#,depth#);
	"The Number 5";
	pair dz[];
	numberpath;	

	z11= point -0.10 of trial;
	dz11=direction -0.10 of trial;
	penpos11(3thin,angle dz11+90);
	pickup pencircle scaled 2.5thin;
	drawdot (z11) ifrotated;

	z12r= point 2.9     of trial;
	dz12=direction 2.8 of trial;
	penpos12(2thin,angle dz12+90);

	z13= point 0.8     of trial;
	penpos13(thin,-90);

	z14r= z13;
	penpos14(3thin,90);
	z15r= (thick,Centerline);
	penpos15(3thin,90);

	z16r= (bearing,nuruuright);
	penpos16(thin,90);

	penstroke (z11e..tension 1.5..z12e{down}..
			tension 3..z13e{dir -30}) ifrotated;
	penstroke (z14e---z15e..z16e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_SIX,numberwidth#+bearing#,height#,depth#);
	"The Number 6";
	numberpath;
	pair dz[];
	% pickup pencircle scaled .25thin;
	% draw trial;

	z11r=point 2.40 of trial;
	dz11=direction 2.40 of trial;
	penpos11(2thin,angle dz11+90);

	z12r=point 2.90 of trial;
	dz12=direction 2.90 of trial;
	penpos12(2thin,angle dz12+90);

	z13 =point 0.23 of trial;
	dz13=direction 0.23 of trial;
	penpos13(2thin,angle dz13+90);
	% pickup pencircle scaled 5thin; drawdot z13;

	z14r=point 0.85 of trial;
	dz14=direction 0.85 of trial;
	penpos14(3thin,angle dz14+90);
	pickup pencircle scaled 3thin; drawdot (z14) ifrotated;

	z15=(z11..z13) intersectionpoint (z12..z14);
	penpos15(thin,angle dz11-90);

	z16=(3/4[x12,x14],nuruuright);
	penpos16(3thin,90);
	z17= z15;
	penpos17(thin,60);

	penstroke (z15e{dz13}..z11e{dz11}..z12e{dz12}
		..z13e{dz13}..z14e{dz14}) ifrotated;
	penstroke (z16e{left}..z17e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_SEVEN,numberwidth#+bearing#,height#,depth#);
	"The Number 7";
	pair dz[];
	path findcenter;
	numberpath;
	% pickup pencircle scaled 0.25thin;
	% draw trial;

	z11l=point -0.6 of trial;
	dz11= direction -0.6 of trial;
	penpos11(3thin,angle dz11-90);
	
	z12l= point 0.45 of trial;
	dz12= direction 0.45 of trial;
	penpos12(1.5thin,angle dz12-90);

	z13 = (w/2,.5[h,-d]); % "Centerpoint"
	
	z14r= point 2.5 of trial;
	dz14=-direction 2.5 of trial;
	penpos14(2thin,angle dz14-90);

	z15r= point 2.1 of trial;
	dz15=-direction 2.1 of trial;
	penpos15(2.25thin,angle dz15-90);

	z16r= point 0.8 of trial;
	dz16=-direction 0.8 of trial;
	penpos16(1.5thin,angle dz16-90);

	findcenter = z11{dz11}..z12{dz12}..tension 1.1..z13..
		{dz14}z14..{dz15}z15..{dz16}z16;
	dz13=-direction 3 of findcenter;
	penpos13(2thin,angle dz13-90);
	
	pickup pencircle scaled 3thin; drawdot (z11) ifrotated;
	pickup pencircle scaled 1.5thin; drawdot (z16) ifrotated;
	
	penstroke (z11e{dz11}..z12e{dz12}..tension 1.1..z13e{dz13}..
		{dz14}z14e..{dz15}z15e..{dz16}z16e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_EIGHT,numberwidth#+bearing#,height#,depth#);
	"The Number 8";
	pair dz[];
	numberpath;	
	pickup pencircle scaled 1/2thin;

	z13 = point 0.8 of trial;
	penpos13(3thin,90);

	z14 = (bearing,gedesinner-thin);
	penpos14(thin,75);

	z15 = (bearing,gedesinner);
	dz15= direction 0.75 of trial;
	penpos15(1/2thin,  -180);

	z16 = (bearing,gedesinner-2thin);
	dz16= direction 0.75 of trial;
	penpos16(2thin,  -180);

	z17 = (bearing,underinner+thin);
	penpos17(2thin,  180);

	z18 = (bearing+1/12bawidth,underinner);
	penpos18(1/2thin,180);

	pickup pencircle scaled 3thin;
	drawdot (z13) ifrotated;
	penstroke (z13e{dir 165}..z14e) ifrotated;
	penstroke (z15e--z16e--z17e--z18e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MONG_NINE,numberwidth#+bearing#,height#,depth#);
	"The Number 9";
	numberpath;
	pair dz[];

	z11r=point 2.40 of trial;
	dz11=direction 2.40 of trial;
	penpos11(1/2thin,angle dz11+90);
	
	z12r=point 2.85 of trial;
	dz12=direction 2.85 of trial;
	penpos12(2thin,angle dz12+90);

	z13 =point 0.23 of trial;
	dz13=direction 0.23 of trial;
	penpos13(2thin,angle dz13+90);

	z14r=point 0.9 of trial;
	dz14=direction 0.9 of trial;
	penpos14(3thin,angle dz14+90);

	z15=(z11..z13) intersectionpoint (z12..z14);
	penpos15(1/2thin,angle dz12+90);

	z16=point 2.2 of trial;
	dz16=direction 2.2 of trial;
	penpos16(2thin,angle dz16);
	z17= z15;
	penpos17(epsilon,30);

	penstroke (z11e{dz11}..z12e{dz12}
		..z13e{dz13}..z14e{dz14}..tension 1.5..z15e) ifrotated;
	penstroke (z16e{up}..z17e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
