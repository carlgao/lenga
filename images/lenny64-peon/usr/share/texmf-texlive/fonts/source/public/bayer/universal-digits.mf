%%
%% This is file `digits.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% universal.dtx  (with options: `digits')
%% 
%% Copyright (C) 1997 Christian Holm.
%% 
%% This file is NOT the source for universal, because almost all
%% comments have been stripped from it.  It is NOT the preferred
%% form of universal for making modifications to it.
%% 
%% Therefore you can NOT redistribute and/or modify THIS file.
%% You can however redistribute the complete source (universal.dtx
%% and universal.ins) and/or modify it under the terms of the GNU
%% General Public License as published by the Free Software
%% Foundation; either version 2, or (at your option) any later
%% version.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public
%% License along with this program; if not, write to the
%%   Free Software Foundation, Inc.,
%%   675 Mass Ave, Cambridge,
%%   MA 02139, USA.
%% 
%% See the file universal.dtx for further comments and documentation.
%% 
bauhausnumb("0",A_width#,l_height#,0,0);
  lft z1 = (0,h-.5wi);
  top z2 = (.5wi,h);
  rt  z3 = (wi,y1);
  rt  z4 = (wi,.5wi);
  bot z5 = (.5wi,0);
  lft z6 = (0,y4);
  draw z1{up}..z2{right}..z3{down}---z4{down}..z5{left}..z6{up}---cycle;
  penlabels(1,2,3,4,5,6);
endchar;

bauhausnumb("1",3/8(A_width#-f_space#)+f_space#,l_height#,0,0);
  top z1 = (0,h);
  top z2 = (2/3wi,h);
  rt  z3 = (wi,h-x2);
  rt  z4 = (wi,0);
  cutdraw z1{right}---z2{right}..z3{down}---z4{down};
  penlabels(1,2,3,4);
endchar;

bauhausnumb("2",A_width#,l_height#,0,0);
  lft     z1 = (0,h-.5wi);
  top     z2 = (.5wi,h);
  rt      z3 = (wi,y1);
  lft bot z4 = (0,0);
      bot z5 = (wi,0);
  cutdraw z1{up}..z2{right}..z3{down}---z4{(-1,1)}---z5{right};
  penlabels(1,2,3,4,5);
endchar;

bauhausnumb("3",A_width#,l_height#,0,0);
  rad = .25h;
  lft z1 = (0,h-rad);
  top z2 = (rad,h);
  top z3 = (wi-rad,h);
  rt  z4 = (wi,h-rad);
  rt  z5 = (wi-rad,.5h);
  rt  z6 = (wi,rad);
  bot z7 = (wi-rad,0);
  bot z8 = (rad,0);
  lft z9 = (0,rad);
  cutdraw z1{up}..z2{right}---z3{right}..z4{down}..z5{left};
  cutdraw z5{right}..z6{down}..z7{left}---z8{left}..z9{up};
  penlabels(1,2,3,4,5,6,7,8,9);
endchar;

bauhausnumb("4",A_width#,l_height#,0,0);
  z1 = (.8wi,h);
  lft z2 = (0,h-x1);
  z3 = (wi,y2);
  z4 = (x1,0);
  z5 = (x1,h-.5x1);
  draw z1{(-1,-1)}---z2{(1,-1)}---z3{right};
  cutoff(z1,90);
  cutoff(z3,0);
  cutdraw z4{up}--z5;
  penlabels(1,2,3,4,5);
endchar;

bauhausnumb("5",A_width#,l_height#,0,0);
  tmp=wi/(1+sind(60));
  lft     z1 = (0,tmp*(1-sind(30)));
  bot     z2 = (wi-tmp,0);
  rt  top z3 = (wi,tmp);
  top     z4 = (x2,2y3);
  top lft z5 = (0,2y3);
  top lft z6 = (0,h);
  top     z7 = (wi,h);
  cutdraw z1{dir(-60)}..z2{right}..z3{up}..z4{left}---
    z5{(-1,1)}---z6{(1,1)}---z7{right};
  penlabels(1,2,3,4,5,6,7);
endchar;

bauhausnumb("6",A_width#,l_height#,0,0);
  lft z1 = (0,.5wi);
  top z2 = (.5wi,wi);
  rt  z3 = (wi,y1);
  bot z4 = (x2,0);
  lft z5 = (0,h-.5wi);
  top z6 = (x2,h);
      z7 = z6 rotatedaround((.5wi,h-.5wi),-60);
  draw  z1{up}..z2{right}..z3{down}..z4{left}..z1{up}---
    z5{up}..z6{right}..z7{dir(-60)};
  cutoff(z7,-60);
  penlabels(1,2,3,4,5,6,7);
endchar;

bauhausnumb("7",A_width#,l_height#,0,0) ;
  top     z1 = (0,h);
  rt  top z2 = (wi,h);
  lft     z3 = (0,0);
  draw z1{right}---z2{(1,-1)}---z3{z3-z1} ;
  cutoff(z1,180);
  cutoff(z3,angle(z3-z1));
  penlabels(1,2,3);
endchar ;

bauhausnumb("8",.5l_height#+p_thick#+f_space#,l_height#,0,0);
  top z1 = (.5wi,h);
      z3 = (x1,.5h);
  bot z6 = (x1,0);
  rt  z2 = bot z3 rotatedaround(.5[z3,z1],90);
  lft z4 = bot z3 rotatedaround(.5[z3,z1],-90);
  rt  z5 = top z3 rotatedaround(.5[z6,z3],-90);
  lft z7 = top z3 rotatedaround(.5[z6,z3],90);
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle;
  draw z3{right}..z5{down}..z6{left}..z7{up}..cycle;
  penlabels(1,2,3,4,5,6,7);
endchar;

bauhausnumb("9",A_width#,l_height#,0,0);
  rt  z1 = (wi,h-.5wi);
  bot z2 = (.5wi,h-wi);
  lft z3 = (0,y1);
  top z4 = (x2,h);
  rt  z5 = (wi,.5wi);
  bot z6 = (x2,0);
      z7 = z6 rotatedaround((.5wi,.5wi),-60);
  draw z1{down}..z2{left}..z3{up}..z4{right}..
    z1{down}---z5{down}..z6{left}..z7{dir(120)};
  cutoff(z7,120);
  penlabels(1,2,3,4,5,6,7);
endchar;


\endinput
%%
%% End of file `digits.mf'.
