%%
%% This is file `specials.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% universal.dtx  (with options: `specials')
%% 
%% Copyright (C) 1997 Christian Holm.
%% 
%% This file is NOT the source for universal, because almost all
%% comments have been stripped from it.  It is NOT the preferred
%% form of universal for making modifications to it.
%% 
%% Therefore you can NOT redistribute and/or modify THIS file.
%% You can however redistribute the complete source (universal.dtx
%% and universal.ins) and/or modify it under the terms of the GNU
%% General Public License as published by the Free Software
%% Foundation; either version 2, or (at your option) any later
%% version.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public
%% License along with this program; if not, write to the
%%   Free Software Foundation, Inc.,
%%   675 Mass Ave, Cambridge,
%%   MA 02139, USA.
%% 
%% See the file universal.dtx for further comments and documentation.
%% 
bauhaussymb(hex"0F",p_thick#+f_space#,l_height#,p_depth#,0);
  "The symbol |";
  lft z1 = (0,h);
  lft z2 = (0,-d);
  cutdraw z1{down}--z2{down};
  penlabels(1,2);
endchar;

bauhaussymb(hex"10",p_thick#+f_space#,x_height#,0,0);
  "Dotless i";
  lft z1 = (0,0);
  lft z2 = (0,h);
  cutdraw z1{up}--z2;
  penlabels(1,2);
endchar;

bauhaussymb(hex"11",p_thick#+f_space#,x_height#,p_depth#,0);
  "Dotless j";
  lft z1 = (0,-d);
  lft z2 = (0,h);
  cutdraw z1{up}--z2;
  penlabels(1,2);
endchar;

bauhaussymb("$",x_width#,l_height#,0,0);
  "The symbol $";
  lft z1  = (0,0.5[y6,y2]) ;
  bot z2  = (1/3wi,1/8h) ;
  bot z3  = (2/3wi,1/8h) ;
  rt  z4  = (wi,0.5[y5,y3]) ;
      z5  = (2/3wi,0.5h) ;
      z6  = (1/3wi,0.5h) ;
  lft z7  = (0,0.5[y8,y6]) ;
  top z8  = (1/3wi,7/8h);
  top z9  = (2/3wi,7/8h) ;
  rt  z10 = (wi,0.5[y8,y6]) ;
  z11     = (.5wi,0);
  z12     = (.5wi,h);
  cutdraw z1{down}..z2{right}--z3{right}..z4{up}..z5{left}--
    z6{left}..z7{up}..z8{right}--z9{right}..z10{down};
  cutdraw z11{up}--z12{up};
  penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

bauhaussymb("&",.5l_height#+2p_thick#+f_space#,l_height#,0,0);
  "The symbol &";
  top z1 = (3/8wi,h);
  bot z6 = (x1,1/8h);
      z3 = .5[z6,z1];
  rt  z2 = bot z3 rotatedaround(.5[z3,z1],90);
  lft z4 = bot z3 rotatedaround(.5[z3,z1],-90);
  rt  z5 = top z3 rotatedaround(.5[z6,z3],-90);
  lft z7 = top z3 rotatedaround(.5[z6,z3],90);
  bot z8 = (wi,1/8h);
      z9 = (x5,0);
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle;
  cutdraw z8{left}---z6{left}..z7{up}..z3{right}..z5{down}---z9{down};
  penlabels(1,2,3,4,5,6,7,8,9);
endchar;

bauhaussymb("?",x_width#,l_height#,0,0);
  "The symbol ?" ;
  lft z1 = (0,h-.5wi);
  top z2 = (.5wi,h);
  rt  z3 = (wi,y1);
  z4 = (x2,h-wi);
  bot z6 = (x2,0);
  z5 = (x2,3y6);
  draw z1{up}..z2{right}..z3{down}..z4{left}--z5{down};
  cutoff(z1,-90);
  cutoff(z5,-90);
  drawdot z6;
  penlabels(1,2,3,4,5,6);
endchar;

bauhaussymb(spanish_query,x_width#,x_height#,p_depth#,0);
  "Spanish query";
  lft z1 = (0,-d+.5wi);
  bot z2 = (.5wi,-d);
  rt  z3 = (wi,y1);
  z4 = (x2,-d+wi);
  top z6 = (x2,h);
  z5 = (x2,h-3*(h-y6));
  draw z3{down}..z2{left}..z1{up}..z4{right}--z5{up};
  cutoff(z3,90);
  cutoff(z5,90);
  drawdot z6;
  penlabels(1,2,3,4,5,6);
endchar;

bauhaussymb(spanish_shriek,p_thick#+f_space#,x_height#,p_depth#,0);
  "Spanish shriek";
  lft top z1 = (0,h);
  lft     z2 = (0,h-3*(h-y1));
  lft     z3 = (0,-d);
  drawdot z1;
  cutdraw z2{down}--z3;
  penlabels(1,2,3);
endchar;

bauhaussymb(hex"80",.7x_width#,l_height#,p_depth#,0);
  "The symbol {";
  top z1 = (wi,h);
  top z2 = (1/2wi,h);
  lft z3 = (1/4wi,h-1/2wi);
  z4     = (x3,1/2(h-d));
  z5     = (0,1/2(h-d));
  z6     = (x3,1/2wi-d);
  bot z7 = (x2,-d);
  bot z8 = (wi,-d);
  cutdraw z1{left}..z3{down}---z4{down}--z5{left};
  cutdraw z4{down}---z6{down}..z8{right};
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhaussymb(hex"81",.7x_width#,l_height#,p_depth#,0);
  "The symbol }";
  top z1 = (0,h);
  top z2 = (1/2wi,h);
  rt z3 = (3/4wi,h-1/2wi);
  z4     = (x3,1/2(h-d));
  z5     = (wi,1/2(h-d));
  z6     = (x3,1/2wi-d);
  bot z7 = (x2,-d);
  bot z8 = (0,-d);
  cutdraw z1{right}..z3{down}---z4{down}--z5{right};
  cutdraw z4{down}---z6{down}..z8{left};
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhaussymb(hex"82",.5l_height#+f_space#,l_height#,0,0);
  "Paragraph sign";
  lft z1 = (0,.5h);
  top z2 = (.5wi,.5h+.5wi);
  rt  z3 = (wi,0.5h);
  bot z4 = (.5wi,.5h-.5wi);
  lft z5 = (0,h-.5wi);
  top z6 = (.5wi,h);
  z7     =  z6 rotatedaround((.5wi,y5),-60);
  rt  z8 = (wi,.5wi);
  bot z9 = (.5wi,0);
  z10    =  z9 rotatedaround((.5wi,y8),-60);
  draw z10{dir(-60)}..z9{right}..z8{up}--z3{up};
  draw z7{dir(120)}..z6{left}..z5{down}--z1{down};
  draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
  penlabels(1,2,3,4,5,6,7,8,9,10);
  cutoff(z10,120);
  cutoff(z7,-30);
endchar;

bauhaussymb(hex"83",A_width#,l_height#,0,0);
  "variant letter Q";
  rt  z1 = (wi,0) ;
  lft z2 = (0,h-.5wi) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,h-.5wi) ;
  bot z5 = (0.5wi,h-wi) ;
  draw z2{up}..z3{right}..z4{down}..z5{left}..cycle ;
  cutdraw z1{up}--z4{up};
  penlabels(1,2,3,4,5);
endchar ;


\endinput
%%
%% End of file `specials.mf'.
