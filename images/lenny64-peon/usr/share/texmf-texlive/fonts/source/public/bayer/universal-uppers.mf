%%
%% This is file `uppers.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% universal.dtx  (with options: `uppers')
%% 
%% Copyright (C) 1997 Christian Holm.
%% 
%% This file is NOT the source for universal, because almost all
%% comments have been stripped from it.  It is NOT the preferred
%% form of universal for making modifications to it.
%% 
%% Therefore you can NOT redistribute and/or modify THIS file.
%% You can however redistribute the complete source (universal.dtx
%% and universal.ins) and/or modify it under the terms of the GNU
%% General Public License as published by the Free Software
%% Foundation; either version 2, or (at your option) any later
%% version.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public
%% License along with this program; if not, write to the
%%   Free Software Foundation, Inc.,
%%   675 Mass Ave, Cambridge,
%%   MA 02139, USA.
%% 
%% See the file universal.dtx for further comments and documentation.
%% 
bauhauschar("A",1A_width#,1l_height#,0,0); %"The letter A";
  lft z1=(0,0);
  lft z2=(0,h-0.5wi);
  top z3=(0.5wi,h);
  rt  z4=(wi,y2);
  rt  z5=(wi,0);
  lft z6=(0,0.5h);
  rt  z7=(wi,y6);
  cutdraw z1{up}--z2{up}..z3{right}..z4{down}--z5{down};
  cutdraw z6{right}--z7{right};
  penlabels(1,2,3,4,5,6,7);
endchar;

bauhauschar("B",1A_width#,1l_height#,0,0); %"The letter B";
  lft bot z1 = (0,0);
  lft top z2 = (0,h);
      top z3 = (wi-0.25h,h);
  rt      z4 = (wi,0.75h);
          z5 = (x3,0.5h);
          z6 = (p_thick,0.5h);
  rt      z7 = (wi,0.25h);
      bot z8 = (x3,0);
  draw z1{(-1,1)}---z2{(1,1)}---z3{right}..z4{down}..z5{left}--z6{left};
  draw z5{right}..z7{down}..z8{left}---z1{(-1,1)};
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhauschar("C",C_width#+f_space#,l_height#,0,0);
  rt  z1 = bot z2 rotatedaround((0.5wi,0.5h),60);
  bot z2 = (0.5wi,0);
  lft z3 = (0,0.5h);
  top z4 = (0.5wi,h);
  rt  z5 = top z4 rotatedaround((0.5wi,0.5h),-60);
  cutdraw z1..z2{left}..z3{up}..z4{right}..z5;
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("D",A_width#,l_height#,0,0);
  lft bot z1 = (0,0);
  lft top z2 = (0,h);
  top     z3 = (wi-.5h,h);
  rt      z4 = (wi,0.5h);
  bot     z5 = (x3,0);
  draw z2{right}---z3{right}..z4{down}..z5{left}---z1{left}---z2{up};
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("E",A_width#,l_height#,0,0);
  rt  bot z1 = (wi,0);
  lft bot z2 = (0,0);
  lft top z3 = (0,h);
  rt  top z4 = (wi,h);
  lft     z5 = (0,0.5h);
  rt      z6 = (wi,0.5h);
  cutdraw z1{left}---z2{(-1,1)}---z3{(1,1)}---z4{right};
  cutdraw z5{left}--z6;
  penlabels(1,2,3,4,5,6);
endchar;

bauhauschar("F",A_width#,l_height#,0,0);
  lft bot z1 = (0,0);
  lft top z2 = (0,h);
  rt  top z3 = (wi,h);
  lft     z4 = (0,0.5h);
  rt      z5 = (wi,0.5h);
  cutdraw z1{up}---z2{(1,1)}---z3{right};
  cutdraw z4{left}--z5;
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("G",l_height#+f_space#,l_height#,0,0);
  rt  z1 = (wi,0.5h);
  bot z2 = (0.5wi,0);
  lft z3 = (0,0.5h);
  top z4 = (0.5wi,h);
  rt  z5 = top z4 rotatedaround((0.5wi,0.5h),-60);
  rt  z6 = (wi,0);
  cutdraw z1..z2{left}..z3{up}..z4{right}..z5;
  cutdraw z1{down}--z6;
  penlabels(1,2,3,4,5,6);
endchar;

bauhauschar("H",A_width#,l_height#,0,0);
  lft z1 = (0,0);
  lft z2 = (0,0.5h);
  lft z3 = (0,h);
  rt  z4 = (wi,0);
  rt  z5 = (wi,0.5h);
  rt  z6 = (wi,h);
  cutdraw z1{up}--z3{up};
  cutdraw z4{up}--z6{up};
  cutdraw z2{left}--z5{left};
  penlabels(1,2,3,4,5,6);
endchar;

bauhauschar("I",p_thick#+2f_space#,l_height#,0,0);
  lft z1 = (0,0);
  lft z2 = (0,h);
  cutdraw z1{up}--z2{up};
  penlabels(1,2);
endchar;

bauhauschar("J",.5x_width#,l_height#,0,0);
  bot    z1 = (0,0);
  bot rt z2 = (wi,0);
      rt z3 = (wi,h);
  cutdraw z1{right}---z2{(1,1)}---z3{up};
  penlabels(1,2,3);
endchar;

bauhauschar("K",0.5l_height#+2f_space#,l_height#,0,0);
  lft     z1 = (0,0);
  lft     z2 = (0,0.5h);
  lft     z3 = (0,h);
  rt      z4 = (wi,0);
  rt      z5 = (wi,h);
  cutdraw z1{up}--z3{up};
  cutdraw z2{right}..z4{down};
  cutdraw z2{right}..z5{up};
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("L",A_width#,l_height#,0,0);
  lft     z1 = (0,h);
  lft bot z2 = (0,0);
  rt  bot z3 = (wi,0);
  cutdraw z1{down}---z2{(1,-1)}---z3{right};
  penlabels(1,2,3);
endchar;

bauhauschar("M",M_width#,l_height#,0,0);
  lft z1 = (0,0) ;
  lft z2 = (0,0.7h) ;
  top z3 = (0.5[x5,x1],h) ;
      z4 = (0.5wi,0.7h-0.25p_thick) ;
      z5 = (0.5wi,0) ;
  top z6 = (0.5[x8,x5],h) ;
  rt  z7 = (wi,0.7h) ;
  rt  z8 = (wi,0) ;
  cutdraw z1{up}--z2..z3{right}..z4{down}--z5 ;
  cutdraw z4{up}..z6{right}..z7--z8{down} ;
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhauschar("N",A_width#,l_height#,0,0);
  lft z1 = (0,0);
  lft z2 = (0,0.6h) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,0.6h) ;
  rt  z5 = (wi,0);
  cutdraw z1{up}--z2{up}..z3{right}..z4{down}--z5{down};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("O",l_height#+f_space#,l_height#,0,0);
  top z1 = (.5wi,h) ;
  rt  z2 = (wi,.5h) ;
  bot z3 = (.5wi,0) ;
  lft z4 = (0,.5h) ;
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle ;
  penlabels(1,2,3,4);
  O_pic = currentpicture;
endchar ;

bauhauschar("P",A_width#,l_height#,0,0);
  lft z1 = (0,0) ;
  lft z2 = (0,h-.5wi) ;
  top z3 = (0.5wi,h) ;
  rt  z4 = (wi,h-.5wi) ;
  bot z5 = (0.5wi,h-wi) ;
  draw z2{up}..z3{right}..z4{down}..z5{left}..cycle ;
  cutdraw z1{up}--z2{up};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("Q",l_height#+f_space#,l_height#,2/3p_depth#,0);
  top z1 = (.5wi,h) ;
  rt  z2 = (wi,.5h) ;
  bot z3 = (.5wi,0) ;
  lft z4 = (0,.5h) ;
  z5     = (.5wi,2d);
  z6     = (.5wi,-d);
  draw z1{right}..z2{down}..z3{left}..z4{up}..cycle ;
  cutdraw z5{down}--z6;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("R",A_width#,l_height#,0,0);
  lft z1 = (0,0);
  lft z2 = (0,h-.5wi);
  top z3 = (.5wi,h);
  rt  z4 = (wi,h-.5wi);
  bot z5 = (0.5wi,h-wi);
  rt  z6 = (wi,h-1.5wi);
  rt  z7 = (wi,0);
  draw z2{up}..z3{right}..z4{down}..z5{left}..cycle;
  cutdraw z1{up}--z2{up};
  cutdraw z5{right}..z7{down};
  penlabels(1,2,3,4,5,6,7);
endchar;

bauhauschar("S",A_width#,l_height#,0,0); %
  lft z1  = (0,0.5[y6,y2]) ;
  bot z2  = (1/3wi,0) ;
  bot z3  = (2/3wi,0) ;
  rt  z4  = (wi,0.5[y5,y3]) ;
      z5  = (2/3wi,0.5h) ;
      z6  = (1/3wi,0.5h) ;
  lft z7  = (0,0.5[y8,y6]) ;
  top z8  = (1/3wi,h);
  top z9  = (2/3wi,h) ;
  rt  z10 = (wi,0.5[y8,y6]) ;
  cutdraw z1{down}..z2{right}--z3{right}..z4{up}..z5{left}
    --z6{left}..z7{up}..z8{right}--z9{right}..z10{down} ;
  penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

bauhauschar("T",A_width#,l_height#,0,0);
  top z1 = (0,h);
  top z2 = (wi,h);
      z3 = (.5wi,h);
      z4 = (0.5wi,0);
  cutdraw z1{right}--z2;
  cutdraw z3{up}--z4;
  penlabels(1,2,3,4);
endchar;

bauhauschar("U",A_width#,l_height#,0,0);
  lft z1 = (0,h);
  lft z2 = (0,0.4h) ;
  bot z3 = (0.5wi,0) ;
  rt  z4 = (wi,0.4h) ;
  rt  z5 = (wi,h);
  cutdraw z1{down}--z2{down}..z3{right}..z4{up}--z5{up};
  penlabels(1,2,3,4,5);
endchar ;

bauhauschar("V",A_width#,l_height#,0,0);
  lft z1 = (0,h);
  bot z2 = (0.5wi-0.5p_thick,0.5p_thick);
  bot z3 = (0.5wi,0);
  bot z4 = (0.5wi+0.5p_thick,0.5p_thick);
  rt  z5 = (wi,h);
  draw z1{down}--z2..z3..z4--z5{up};
  cutoff(z1,90);
  cutoff(z5,90);
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("W",M_width#,l_height#,0,0);
  lft z1 = (0,h) ;
  lft z2 = (0,0.3h) ;
  bot z3 = (0.5[x5,x1],0) ;
      z4 = (0.5wi,0.3h+0.25p_thick) ;
      z5 = (0.5wi,h) ;
  bot z6 = (0.5[x8,x5],0) ;
  rt  z7 = (wi,0.3h) ;
  rt  z8 = (wi,h) ;
  cutdraw z1{down}--z2..z3{right}..z4{up}--z5 ;
  cutdraw z4{down}..z6{right}..z7--z8{down} ;
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhauschar("X",l_height#+f_space#,l_height#,0,0);
  top z1 = (0,h);
      z2 = (0.5wi,0.5wi);
  bot z3 = (0,0);
  top z4 = (wi,h);
  bot z5 = (wi,0);
  cutdraw z1{right}..z2{down}..z3{left};
  cutdraw z4{left}..z2{down}..z5{right};
  penlabels(1,2,3,4,5);
endchar;

bauhauschar("Y",A_width#,l_height#,0,0) ;
  rt  z1 = (wi,0) ;
  lft z2 = (0,h) ;
  rt  z3 = (wi,h) ;
  rt  z4 = (wi,h-x5) ;
  bot z5 = (0.5wi,h-wi) ;
  lft z6 = (0,h-x5) ;
  cutdraw z1{up}--z3 ;
  cutdraw z3{up}--z4..z5{left}..z6{up}--z2{up} ;
  penlabels(1,2,3,4,5,6);
endchar ;

bauhauschar("Z",A_width#,l_height#,0,0) ;
  top z1 = (0,h);
  rt  x2 = wi        ; top y2 = h ;
  rt  x3 = wi        ; top y3 = y2;
  lft x4 = 0         ; bot y4 = y5 ;
  lft x5 = 0         ; bot y5 = 0 ;
  bot z6 = (wi,0);
  cutdraw z1{right}--z2..z3--z4..z5--z6 ;
  penlabels(1,2,3,4,5,6);
endchar ;


\endinput
%%
%% End of file `uppers.mf'.
