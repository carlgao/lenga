%%
%% This is file `punctuations.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% universal.dtx  (with options: `punctuations')
%% 
%% Copyright (C) 1997 Christian Holm.
%% 
%% This file is NOT the source for universal, because almost all
%% comments have been stripped from it.  It is NOT the preferred
%% form of universal for making modifications to it.
%% 
%% Therefore you can NOT redistribute and/or modify THIS file.
%% You can however redistribute the complete source (universal.dtx
%% and universal.ins) and/or modify it under the terms of the GNU
%% General Public License as published by the Free Software
%% Foundation; either version 2, or (at your option) any later
%% version.
%% 
%% The universal font and package is distributed in the hope that
%% it will be useful, but WITHOUT ANY WARRANTY; without even the
%% implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
%% PURPOSE.  See the GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public
%% License along with this program; if not, write to the
%%   Free Software Foundation, Inc.,
%%   675 Mass Ave, Cambridge,
%%   MA 02139, USA.
%% 
%% See the file universal.dtx for further comments and documentation.
%% 
bauhaussymb("!",p_thick#+f_space#,l_height#,0,0);
  "The punctuation !";
  lft bot z1 = (0,0);
  lft     z2 = (0,3y1);
  lft     z3 = (0,h);
  drawdot z1;
  cutdraw z2{up}--z3;
  penlabels(1,2,3);
endchar;

bauhaussymb("(",.5x_width#,l_height#,p_depth#,0);
  "The punctuation (";
  rt  z1 = (wi,h);
  lft z3 = (0,.5(h-d));
  rt  z5 = (wi,-d);
  draw z1..z3..z5;
  cutoff(z1,90);
  cutoff(z5,-90);
  penlabels(1,3,5);
endchar;

bauhaussymb(")",.5x_width#,l_height#,p_depth#,0);
  "The punctuation )";
  lft z1 = (0,h);
  rt  z3 = (wi,.5(h-d));
  lft z5 = (0,-d);
  draw z1..z3..z5;
  cutoff(z1,90);
  cutoff(z5,-90);
  penlabels(1,3,5);
endchar;

bauhaussymb(hex"22",3f_space#,l_height#,0,0);
  "The punctuation ''";
  lft z1  = (3p_thick,h);
  lft z2  = (p_thick,h);
  lft z3 = (2p_thick,h-3p_thick);
  lft z4 = (0,h-3p_thick);
  draw z1--z3;
  draw z2--z4;
  cutoff(z1,90);
  cutoff(z2,90);
  cutoff(z3,-90);
  cutoff(z4,-90);
  penlabels(1,2,3,4);
endchar;

bauhaussymb("#",A_width#,l_height#,0,0);
  "The punctuation #";
  lft top z1 = (0,3/4h);
  lft bot z2 = (0,1/4h);
  rt  z3 = (wi,y1);
  rt  z4 = (wi,y2);
  cutdraw z1{right}--z3{right};
  cutdraw z2{right}--z4{right};
  bot z5 = (1/5wi,0);
  bot z6 = (3/5wi,0);
  top z7 = (2/5wi,h);
  top z8 = (4/5wi,h);
  draw z5--z7;
  draw z6--z8;
  cutoff(z5,-90);
  cutoff(z6,-90);
  cutoff(z7,90);
  cutoff(z8,90);
  penlabels(1,2,3,4,5,6,7,8);
endchar;

bauhaussymb("%",A_width#,l_height#,0,0);
  "The punctuation %";
  rt  z1  = (wi,h);
  lft z2  = (0,0);
  draw z1--z2;
  cutoff(z1,90);
  cutoff(z2,-90);
  top z3  = (1/4wi,h);
  rt  z4  = top z3 rotatedaround((x3,h-x3),-90);
  bot z5  = top z3 rotatedaround((x3,h-x3),-180);
  lft z6  = top z3 rotatedaround((x3,h-x3),-270);
  draw z3{right}..z4{down}..z5{left}..z6{up}..cycle;
  bot z7  = (3/4wi,0);
  lft z8  = bot z7 rotatedaround((x7,x3),-90);
  top z9  = bot z7 rotatedaround((x7,x3),-180);
  rt  z10 = bot z7 rotatedaround((x7,x3),-270);
  draw z7{left}..z8{up}..z9{right}..z10{down}..cycle;
  penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

bauhaussymb("'",2f_space#,l_height#,0,0);
  "The punctuation '";
  lft z1 = (p_thick,h);
  lft z2 = (0,h-3p_thick);
  draw z1--z2;
  cutoff(z1,90);
  cutoff(z2,-90);
  penlabels(1,2);
endchar;

bauhaussymb("*",x_width#,l_height#,0,0);
  "The punctuation *";
  z1 = (0.5wi,h);
  z2 = z1 rotatedaround((.5wi,h-.5wi),-60);
  z3 = z1 rotatedaround((.5wi,h-.5wi),-120);
  z4 = z1 rotatedaround((.5wi,h-.5wi),-180);
  z5 = z1 rotatedaround((.5wi,h-.5wi),-240);
  z6 = z1 rotatedaround((.5wi,h-.5wi),-300);
  cutdraw z1--z4;
  cutdraw z2--z5;
  cutdraw z3--z6;
  penlabels(1,2,3,4,5,6);
endchar;

bauhaussymb("+",x_width#,x_height#,0,0);
  "The punctuation +";
  z1 = (.5wi,h);
  z2 = (.5wi,0);
  z3 = (0,.5h);
  z4 = (wi,.5h);
  cutdraw z1{down}--z2;
  cutdraw z3{right}--z4;
  penlabels(1,2,3,4);
endchar;

bauhaussymb(",",p_thick#+f_space#,p_thick#,2/3p_depth#,0);
  "The punctuation ,";
  lft bot z1 = (0,h);
  lft top z2 = (0,-d);
  cutdraw z1{down}--z2;
  penlabels(1,2);
endchar;

bauhaussymb("-",.7x_width#,x_height#,0,0);
  "The punctuation -";
  z1 = (0,.5h);
  z2 = (wi,y1);
  cutdraw z1{right}--z2;
  penlabels(1,2);
endchar;

bauhaussymb(".",p_thick#+f_space#,p_thick#,0,0);
  "The punctuation .";
  lft bot z1 = (0,0);
  drawdot z1;
  penlabels(1);
endchar;

bauhaussymb("/",2/3x_width#,l_height#,p_depth#,0);
  "The punctuation /";
  lft z1 = (0,-d);
  rt  z2 = (wi,h);
  draw z1--z2;
  cutoff(z1,-90);
  cutoff(z2,90);
  penlabels(1,2);
endchar;

bauhaussymb(":",p_thick#+f_space#,x_height#,0,0);
  "The punctuation :";
  top lft z1 = (0,h);
  bot lft z2 = (0,0);
  drawdot z1;
  drawdot z2;
  penlabels(1,2);
endchar;

bauhaussymb(";",p_thick#+f_space#,x_height#,2/3p_depth#,0);
  "The punctuation ;";
  lft bot z1 = (0,0);
  lft top z2 = (0,-d);
  lft top z3 = (0,h);
  cutdraw z1{down}--z2;
  drawdot z3;
  penlabels(1,2,3);
endchar;

bauhaussymb("=",.7x_width#,x_height#,0,0);
  "The punctuation =";
  top z1 = (0,1/3h);
  z2 = (wi,y1);
  cutdraw z1{right}--z2;
  bot z3 = (0,2/3h);
  z4 = (wi,y3);
  cutdraw z3{right}--z4;
  penlabels(1,2,3,4);
endchar;

bauhaussymb("@",l_height#+f_space#,l_height#,0,0);
  "The punctuation @";
  rt  z1 = (wi,0);
  rt  z2 = (wi,.5h);
  top z3 = (.5wi,h);
  lft z4 = (0,.5h);
  z5     = z4 rotatedaround((.5wi,.5h),60);
  rt  z6 = (wi,1/3h);
  top z7 = (wi-1/3h,2/3h);
  lft z8 = (wi-2/3h,1/3h);
  bot z9 = (wi-1/3h,0);
  cutdraw z1{up}--z2{up}..z3{left}..z4{down}..z5{dir(-30)};
  draw z6{up}..z7{left}..z8{down}..z9{right}..cycle;
  penlabels(1,2,3,4,5,6,7,8,9);
endchar;

bauhaussymb("[",.7x_width#,l_height#,p_depth#,0);
  "The punctuation [";
  rt  top z1 = (wi,h);
  lft top z2 = (0,h);
  lft bot z3 = (0,-d);
  rt  bot z4 = (wi,-d);
  cutdraw z1{left}---z2{(-1,-1)}---z3{(1,-1)}---z4{right};
  penlabels(1,2,3,4);
endchar;

bauhaussymb(hex"5C",3f_space#,l_height#,0,0);
  "The punctuation ``";
  lft z1  = (0,h);
  lft z2  = (2p_thick,h);
  lft z3 = (p_thick,h-3p_thick);
  lft z4 = (3p_thick,h-3p_thick);
  draw z1--z3;
  draw z2--z4;
  cutoff(z1,90);
  cutoff(z2,90);
  cutoff(z3,-90);
  cutoff(z4,-90);
  penlabels(1,2,3,4);
endchar;

bauhaussymb("]",.7x_width#,l_height#,p_depth#,0);
  "The punctuation ]";
  lft top z1 = (0,h);
  rt  top z2 = (wi,h);
  rt  bot z3 = (wi,-d);
  lft bot z4 = (0,-d);
  cutdraw z1{right}---z2{(1,-1)}---z3{(-1,-1)}---z4{left};
  penlabels(1,2,3,4);
endchar;

bauhaussymb("`",2f_space#,l_height#,0,0);
  "The punctuation `";
  lft z1 = (0,h);
  lft z2 = (p_thick,h-3p_thick);
  draw z1--z2;
  cutoff(z1,90);
  cutoff(z2,-90);
  penlabels(1,2);
endchar;

bauhaussymb(hex"7B",x_width#,x_height#,0,0);
  "The punctuation --";
  z1 = (0,.5h);
  z2 = (wi,y1);
  cutdraw z1{right}--z2;
  penlabels(1,2);
endchar;

bauhaussymb(hex"7C",m_width#,x_height#,0,0);
  "The punctuation ---";
  z1 = (0,.5h);
  z2 = (wi,y1);
  cutdraw z1{right}--z2;
  penlabels(1,2);
endchar;


\endinput
%%
%% End of file `punctuations.mf'.
