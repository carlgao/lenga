%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%        File: maglyphs.mf
%      Author: Oliver Corff
%        Date: October 1st, 2001
%     Version: 0.8
%   Copyright: Ulaanbaatar, Beijing, Berlin
%
% Description: Local Manju Script (LMA)
%              Manju Bithe Glyph Definitions
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
	writing:=Bithe;
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TASLAL,tewidth#,height#,depth#);
	"Manju Taslal Temdeg";
	manjudusal(2/4tewidth,Centerline,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_FULLSTOP,bawidth#,height#,depth#);
	"Manju Full Stop";
	manjudusal(5/16bawidth,Centerline,3thin,2thick);
	ManjuDusal(11/16bawidth,Centerline,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_E,shudwidth#,height#,depth#);
	"Manju letter e, dotted shud";
	shud(0);
	dusal (1/2w,underinner+thin,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DUBENE,shudwidth#,height#,depth#);
	"Manju letter e in final position";
	suul;
	dusal (12/12w,underinner+thin,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_U,gedeswidth#,height#,depth#);
	"Manju letter u, dotted gedes";
	nuruu(w,0);
	z1r=(w-bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(bearing,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;
	penlabels(1,2,3);
	dusal (1/2gedeswidth,underinner+thin,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DUBENU,eswidth#,height#,depth#);
	"Manju letter final u";
	nuruu(w,w-numtaigedeswidth);

	z1r=(w-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(w-numtaigedeswidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(1/2[x4,x6],underouter);
	z6r=(0,nuruuright);	% nuruuright was Centerline
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(1/4thin,-180);
	penstroke (z4e{down}..z5e..{up}z6e) ifrotated;
	penlabels(1,2,3,4,5,6);
	dusal (17/36w,1.6underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_UU,gedeswidth#+shilbewidth#,height#,depth#);
	"Manju letter ^u";
	nuruu(w,0);
	z1r=(w-bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(bearing+shilbewidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;
	z4r=(shilbewidth,nuruuleft);
	z5r=(+3thin,shilbeheight);
	penpos4(2thin,0);
	penpos5(4thin,60);
	penstroke (z4e--z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DUBENUU,gedeswidth#+shudwidth#,height#,depth#);
	"Manju letter ^u, final form";
	nuruu(w,shudwidth);
	z1r=(w-bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(bearing+shudwidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;
	z4=(shudwidth,Centerline);
	z5=(1/2shudwidth,Centerline);
	z6=(0,nuruuleft+thin);
	penpos4(nuruuwidth,90);
	penpos5(14/16nuruuwidth,80);
	penpos6(epsilon,45);
	penstroke (z4e---z5e..z6e) ifrotated;
	penlabels(1,2,3,4,5,6);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNGA,tewidth#,height#,depth#);
	"Manju letter initial ga";
	xewteeqix;
	dusal(-1/2shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNHA,tewidth#,height#,depth#);
	"Manju letter initial ha";
	xewteeqix;
	dugui(-1/2shudwidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_G,2shudwidth#,height#,depth#);
	"Manju letter medial g";
	shud(0);
	shud(shudwidth);
	dusal(shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_H,2shudwidth#,height#,depth#);
	"Manju letter medial ha";
	shud(0);
	shud(shudwidth);
	dugui(shudwidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GE,tewidth#,height#,depth#);
	"Manju Ligature GE";
	ligka;
	dusal (21/36w,1.35underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HE,tewidth#,height#,depth#);
	"Manju Ligature: HE";
	ligka;
	dugui (21/36w,1.6underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GI,eswidth#+secAwidth#,height#,depth#);
	"Manju Manju Ligature: GI";
	ligki;
	dusal (22/36w,1.35underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HI,eswidth#+secAwidth#,height#,depth#);
	"Manju Manju Ligature: HI";
	ligki;
	dugui (22/36w,1.6underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GU,bawidth#,height#,depth#);
	"Manju Ligature: GU";
	ligku;
	dusal (18/36w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HU,bawidth#,height#,depth#);
	"Manju Ligature: HU";
	ligku;
	dugui (18/36w,1.9underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_KH,gedeswidth#,height#,depth#);
	"Manju letter kh";
	qagt;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_KHA,bawidth#,height#,depth#);
	"Manju Ligature: KHA";
	ligkha;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_KHO,bawidth#,height#,depth#);
	"Manju Ligature: KHO";
	ligkhu;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GH,gedeswidth#,height#,depth#);
	"Manju letter GH";
	qagt;
	dusal (15/36w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GHA,bawidth#,height#,depth#);
	"Manju Ligature: GHA";
	ligkha;
	dusal (21/36w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_GHO,bawidth#,height#,depth#);
	"Manju Ligature: GHO";
	ligkhu;
	dusal (19/36w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HH,gedeswidth#,height#,depth#);
	"Manju letter HH";
	qagt;
	dugui (15/36w,1.9underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HHA,bawidth#,height#,depth#);
	"Manju Ligature: HHA";
	ligkha;
	dugui (21/36w,1.9underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HHO,bawidth#,height#,depth#);
	"Manju Ligature: HHO";
	ligkhu;
	dugui (18/36w,1.9underouter,1.5thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DUBENK,2shudwidth#,height#,depth#);
	"Manju Final k";
	shud(shudwidth);
	bagaodoisuul;
	dusal(9/12shudwidth,gedesouter,3thin,2thick);
	dusal(3/12shudwidth,gedesouter,3thin,2thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_BE,bawidth#,height#,depth#);
	"Manju Manju Ligature: BE";
	ligba;
	dusal (21/36w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_BU,2numtaigedeswidth#,height#,depth#);
	"Manju Ligature: BU";
	ligbu(0);
	dusal (1/2w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_BUU,2numtaigedeswidth#+shilbewidth#,height#,depth#);
	"Manju Ligature: BUU";
	ligbu(shilbewidth);
	nuruu(shilbewidth,0);
	z11r=(shilbewidth,nuruuleft);
	z12r=(+3thin,shilbeheight);
	penpos11(2thin,0);
	penpos12(4thin,60);
	penstroke (z11e--z12e) ifrotated;
	penlabels(1,2,3,4,5,11,12);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_D,eswidth#,height#,depth#);
	"Manju letter de";
	roundgedestshilbe;
	dusal(1/2shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_T,eswidth#,height#,depth#);
	"Manju letter te";
	roundgedestshilbe;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUND,cewidth#,height#,depth#);
	"Manju letter initial D";
	deribun_ta;
	dusal(1/2shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNT,cewidth#,height#,depth#);
	"Manju letter initial t";
	deribun_ta;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DE,eswidth#,height#,depth#);
	"Manju letter de";
	manju_te;
	dusal(1/2shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TE,eswidth#,height#,depth#);
	"Manju letter te";
	manju_te;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNDE,cewidth#,height#,depth#);
	"Manju letter De";
	deribun_te;
	dusal(-1/4shudwidth,underinner+thin,2thick,1thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNTE,cewidth#,height#,depth#);
	"Manju initial letter Te";
	deribun_te;
	penlabels(1,2,3,4);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TIBT,eswidth#,height#,depth#);
	"Manju initial letter Te";
	nuruu(w,0);
	z1r=(w-1/2bearing,nuruuleft);
	z2 =(07/16w,orxicouter);
	penpos1(2thin,0);
	penpos2(2thin,45);
	penstroke (z1e--z2e) ifrotated;
	z3r=z2;
	z4r=(bearing,7/12[y1,y2]);

	penpos3(2thin,115 );
	penpos4(2thin,210);
	
	z5 =(6/16w,3/12[y1,y2]);
	z6 =(x1-2u,nuruuleft);

	penpos5(1.5thin,230);
	penpos6(1.5thin,180);
	penstroke (z3e{dir 205}..tension 2.5..z4e..tension 2.5..
		z5e..tension infinity..z6e)
		ifrotated;
	penlabels(1,2,3,4,5,6);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_SH,tewidth#,height#,depth#);
	"Manju letter `sh'";
	zawj;
	manju_sh_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNSH,tewidth#,height#,depth#);
	"Manju letter `sh' in initial position";
	exenzawj;
	manju_sh_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DUBENSH,tewidth#,height#,depth#);
	"Manju letter `sh' in final position";
	suulzawj;
	manju_sh_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DZ,tewidth#,height#,depth#);
	"Manju letter `dz'";
	zawj;
	manju_dz_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNDZ,tewidth#,height#,depth#);
	"Manju letter `dz' in initial position";
	exenzawj;
	manju_dz_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DZI,tewidth#,height#,depth#);
	"Manju ligature `dzi'";
	manju_si;
	manju_dz_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNDZI,tewidth#,height#,depth#);
	"Manju letter `dzi' in initial position";
	manju_deribun_si;
	manju_dz_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TS,tewidth#,height#,depth#);
	"Manju letter `ts'";
	zawj;
	manju_dz_stroke;
	dugui(3/5tewidth,1.1suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNTS,tewidth#,height#,depth#);
	"Manju letter `ts' in initial position";
	exenzawj;
	manju_dz_stroke;
	dugui(3/5tewidth,1.1suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TSH,tewidth#,height#,depth#);
	"Manju letter `tsh'";
	zawj;
	manju_dz_stroke;
	manju_tsh_stroke;
	tsh_picture:=currentpicture;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNTSH,tewidth#,height#,depth#);
	"Manju letter `tsh' in initial position";
	exenzawj;
	manju_dz_stroke;
	manju_tsh_stroke;
	deribuntsh_picture:=currentpicture;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TSHY,shudwidth#+tewidth#,height#,depth#);
	"Manju letter `tshy'";
	currentpicture:= tsh_picture shifted
			  if LR: (0,0) else: (shudwidth,0) fi;
	sy_nuruu(shudwidth,0);
%	manju_dz_stroke;
%	manju_tsh_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNTSHY,shudwidth#+tewidth#,height#,depth#);
	"Manju letter `tshy' in initial position";
	currentpicture:= deribuntsh_picture shifted
			  if LR: (0,0) else: (shudwidth,0) fi;
	sy_nuruu(shudwidth,0);
%	manju_dz_stroke;
%	manju_tsh_stroke;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_Z,tewidth#,height#,depth#);
	"Manju letter `z'";
	zawj;
	dugui(3/5tewidth,suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNZ,tewidth#,height#,depth#);
	"Manju letter `z' in initial position";
	exenzawj;
	dugui(3/5tewidth,suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_ZH,tewidth#,height#,depth#);
	"Manju letter `zh'";
	zawj;
	manju_sh_stroke;
	dugui(3/5tewidth,suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNZH,tewidth#,height#,depth#);
	"Manju letter `zh' in initial position";
	exenzawj;
	manju_sh_stroke;
	dugui(3/5tewidth,suulouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_NGA,shudwidth#,height#,depth#);
	"Manju (Tibetan) letter `ng'";
	shud(0);
	dugui(1/2shudwidth,yashilbeheight,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNNGA,shudwidth#,height#,depth#);
	"Manju (Tibetan) letter `ng' in initial position";
	titem;
	dugui(1/2shudwidth,yashilbeheight,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_JY,eswidth#,height#,depth#);
	"Manju letter jy; bosoo_shilbe with circle";
	bosoo_shilbe;
	dugui(1/2eswidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNJY,shilbewidth#,height#,depth#);
	"Manju letter jy; shilbe with circle";
	shilbe(0);
	dugui(1/2shilbewidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_CY,eswidth#,height#,depth#);
	"Manju letter cy";
	seree_ewer;
	dugui(1/2eswidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_SY,tewidth#+shudwidth#,height#,depth#);
	"Manju letter sy";
	currentpicture:= zawjpicture shifted
			  if LR: (0,0) else: (shudwidth,0) fi;
	sy_nuruu(shudwidth,0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNSY,tewidth#+shudwidth#,height#,depth#);
	"Manju letter initial sy";
	currentpicture:= exenzawjpicture shifted
			  if LR: (0,0) else: (shudwidth,0) fi;
	sy_nuruu(shudwidth,0);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_ZR,shudwidth#,height#,depth#);
	"Manju letter zr";
	nuruu(shudwidth,0);
	z1 =(1/2shudwidth,gedesinner);
	z2 =(1/2shudwidth,underinner);
	penpos1(2.5thin,-15);
	penpos2(2.5thin,-15);
	penstroke (z1e--z2e) ifrotated;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNZR,shudwidth#,height#,depth#);
	"Manju letter initial zr";
	nuruu(1/2shudwidth,0);
	z1r=(1/2shudwidth,gedesinner);
	z2r=(1/2shudwidth,underinner);
	penpos1(2.5thin,-15);
	penpos2(2.5thin,-15);
	penstroke (z1e--z2e) ifrotated;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_LH,shudwidth#,height#,depth#);
	"Manju Tibetan letter lh";
	shud(0);
	l_ewer(1/2shudwidth);
	dugui(-1/2shudwidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
if style=wood:  exenlwidth#:=numtaigedeswidth#+shudwidth# fi;
if style=steel: exenlwidth#:=shudwidth# fi;

beginchar(MANJU_DERIBUNLH,shudwidth#,height#,depth#);
	"Manju Tibetan letter initial lh";
	if style=wood:
		"Style: Wood";
		titem_stroke(shudwidth);
		shud(0);
		l_ewer(1/2shudwidth);
	fi;
	if style=steel:
		"Style: Steel";
		titem;
		l_ewer(1/2shudwidth);
	fi;
	dugui(-1/2shudwidth,underinner,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_HIGHLH,shudwidth#,height#,depth#);
	"Manju Tibetan letter lh";
	shud(0);
	l_ewer(1/2shudwidth);
	dugui(-1/2shudwidth,underouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
if style=wood:  exenlwidth#:=numtaigedeswidth#+shudwidth# fi;
if style=steel: exenlwidth#:=shudwidth# fi;

beginchar(MANJU_HIGHDERIBUNLH,shudwidth#,height#,depth#);
	"Manju Tibetan letter initial lh";
	if style=wood:
		titem_stroke(shudwidth);
		shud(0);
		l_ewer(1/2shudwidth);
	fi;
	if style=steel:
		titem;
		l_ewer(1/2shudwidth);
	fi;
	dugui(-1/2shudwidth,underouter,1.5thick)
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_F,cewidth#,height#,depth#);
	"Manju letter f";
	nuruu(cewidth,0);	
	z1 =(1/8gedeswidth,gedesinner);
	z2r=(3/8gedeswidth,yashilbeheight);
	z3 =(7/8gedeswidth-1/4bearing,nuruuleft); % was 8/8gedeswidth
	penpos1(epsilon,+135);
	penpos2(3.75thin,+90);
	penpos3(1.50thin,0);
	z4 =(10/8gedeswidth,underinner-thin);	% was 11/8
	z5 =(12/8gedeswidth,underinner);	% was 13/8
	penpos4(3.25thin,  90);
	penpos5( epsilon,+135);
	penstroke (z1e..tension 2.50..z2e..tension 3.5..z3e
		 ..tension 3.5..z4e..tension 1.75..z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_DERIBUNF,cewidth#,height#,depth#);
	"Manju letter initial f";
	nuruu(gedeswidth+1/3bearing,0);	
	z1 =(1/8gedeswidth,gedesinner);
	z2r=(3/8gedeswidth,yashilbeheight);
	z3 =(gedeswidth-1/4bearing,nuruuleft);
	penpos1(epsilon,+135);
	penpos2(3.75thin,+90);
	penpos3(1.50thin,0);
	z4 =(11/8gedeswidth,underinner);
	z5 =(13/8gedeswidth,underinner+thin);
	penpos4(3.25thin,  90);
	penpos5( epsilon,+135);
	penstroke (z1e..tension 2.50..z2e..tension 3.5..z3e
		 ..tension 3.5..z4e..tension 1.75..z5e) ifrotated;
	penlabels(1,2,3,4,5);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_P,eswidth#,height#,depth#);
	"Manju letter `p'";
	nuruu(eswidth,eswidth-numtaigedeswidth);

	z1r=(eswidth-1/2bearing,nuruuleft);
	z2r=(1/2[x1,x3],gedesouter);
	z3r=(eswidth-numtaigedeswidth,nuruuleft);
	penpos1(thin,0);
	penpos2(gedesouter-gedesinner,90);
	penpos3(thin,180);
	penstroke (z1e{up}..z2e..{down}z3e) ifrotated;

	z4 =(x1,nuruuright);
	z5r=(1/4[x4,x8],underouter);
	z6 =(1/2[x4,x8],0);
	z7l=(3/4[x4,x8],underouter);
	z8l=(0,Centerline);
	penpos4(thin,0);
	penpos5(underinner-underouter,-90);
	penpos6(1/4thin,-180);
	penpos7(3/4(underinner-underouter),-270);
	penpos8(1/2thin, 0);
	penstroke (z4e{down}..z5e..{up}z6e) ifrotated;
	penstroke (z6e{down}..z7e..tension 1.2..{up}z8e) ifrotated;
	penlabels(1,2,3,4,5,6,7,8);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_PA,bawidth#,height#,depth#);
	"Manju ligature `pa'";
	manju_pa;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_PE,bawidth#,height#,depth#);
	"Manju ligature `pe'";
	manju_pa;
	dusal (22/36bawidth,1.85underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_PI,biwidth#,height#,depth#);
	"Manju ligature `pi'";
	manju_pi;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_PO,bawidth#,height#,depth#);
	"Manju ligature `po'";
	manju_po;
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_PU,bawidth#,height#,depth#);
	"Manju ligature `pu'";
	manju_po;
	dusal (1/2w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TIBPE,bawidth#,height#,depth#);
	"Manju ligature p'e";
	ligmongpa;
	dusal (1/2w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
beginchar(MANJU_TIBPU,bawidth#,height#,depth#);
	"Manju ligature p'u";
	ligmongpu;
	dusal (1/2w,1.65underouter,2thick,1thick);
endchar;
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
