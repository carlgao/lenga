%%
%% This is file `sqrcdig.mf',
%% generated with the docstrip utility.
%%
%% The original source files were:
%%
%% sqrcaps.dtx  (with options: `dig')
%% 
%%  Copyright 1999 Peter R. Wilson
%% 
%%  This program is provided under the terms of the
%%  LaTeX Project Public License distributed from CTAN
%%  archives in directory macros/latex/base/lppl.txt.
%% 
%% Author: Peter Wilson (CUA)
%%         now at: peter.r.wilson@boeing.com
%% 

cmchar "SQRC digit 1";
beginchar("1", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric n[];
n1 := sqrcjut;
pickup quill;
x1=x2=w/2;
top y1=h; bot y2=0;
%%ductus:=true;
draw z1--z2;
draw (x1-n1,y1)--(x1,y1);
draw (x2,y2)--(x2+n1,y2);
ductus:=false;
penlabels(1,2,100);
endchar; % end "1"

cmchar "SQRC digit 2";
beginchar("2", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
numeric n[];
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
pcshiftx := u;
pickup quill;
lft x1=pcshiftx+o; top y1=h-2thin;
x2=1/2w; top y2= h+o;
rt x3=w-pcshiftx-o; y3=3/4h;
lft x5=pcshiftx; bot y5=0;
rt x6=w-pcshiftx; y6=y5;
x6a = rt x6; y6a = y6;
x6b = x6a; y6b = y6a+n2;
x6c = x6a-n2; y6c=y6a;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z3{down}..{-pdir}z5;    % curve
draw z5--z6;                                % foot
pickup stylus;
filldraw z6a--z6b{down}..{left}z6c--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "2"

cmchar "SQRC digit 3";
beginchar("3", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x3=pcshiftx; x10=x3;
rt x5=w-pcshiftx; x8=x5-3thin;
x4=0.5[x3,x5];
x9=0.5[x10,x8];
%%lft x6=pcshiftx+3thin; x7=x6;
x6=1/2[x3,x9]; x7=x6;
bot y3=thin; top y10=h-2thin;
y6=0.5[y3,y10]; y7=y6;
bot y4=-thin; top y9=h+thin;
y5=0.5[y3,y6];
y8=11/12[y7,y10];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
%%draw z3..z4{right}..z5{up}...{-pdir}z6;                 % bottom bowl
%%draw z7{pdir}..z8{up}..z9{left}..z10;                   % top bowl
draw z3..z4{right}..z5{up}...{left}z6;                 % bottom bowl
draw z7{right}..z8{up}..z9{left}..z10;                   % top bowl
ductus:=false;
penlabels(1,2,3,4,5,6,7,8,9,10,100);
endchar; % end "3"

cmchar "SQRC digit 4";
beginchar("4", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric n[];
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
pickup quill;
rt x1= rt x2= w-pcshiftx-n1;
top y1=h; bot y2=0;
lft x3=pcshiftx; y3=0.33h;
rt x4=w-pcshiftx; y4=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;               % vertical
draw (x2,y2)--(x2+n1,y2);
draw z3--z4;               % horizontal
draw z1--z3;               % diagonal
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "4"

cmchar "SQRC digit 5";
beginchar("5", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric n[];
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
pickup quill;
rt x1=w-pcshiftx; top y1=h;
lft x2=pcshiftx; y2=y1;
x3=x2; y3=0.5h+thin;
x4=0.5[x1,x3]; y4=0.5[y3,y5];
rt x5=w-pcshiftx; y5=0.3h;
x6=w/2; bot y6=0;
lft x7=pcshiftx; bot y7=2thin;
x1a = rt x1; y1a = y1c = y1;
x1b = x1a; y1b = y1a-n2;
x1c = x1a-n2;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;                              % horizontal
draw z2--z3;                              % vertical
draw z3{pdir}..z5{down}..z6{left}..z7;    % curve
pickup stylus;
%%filldraw z1a--z1b{up}..{left}z1c--cycle;
filldraw z1a--z1c{right}..{down}z1b--cycle;
ductus:=false;
penlabels(1,1a,1b,1c,2,3,4,5,6,7,100);
endchar; % end "5"

cmchar "SQRC digit 6";
beginchar("6", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
rt x1=w-pcshiftx; top y1=h-2thin;
x2=1/2w; top y2= h;
lft x3=pcshiftx; y3=1/2h;
x4=x2; bot y4=0;
rt x5=w-pcshiftx; y5=1/4h;
lft x6=rt x3; bot y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{left}..z3{down}..z4{right}..z5{up}..{-pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "6"

cmchar "SQRC digit 7";
beginchar("7", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric n[];
n1 := sqrcjut;
n2 := sqrcjutrat*sqrcjut;
pickup quill;
lft x1=pcshiftx;
rt x2=w-pcshiftx;
top y1=top y2=h;
x1a = lft x1; y1a = y1c = y1;
x1b = x1a; y1b = y1a-n2;
x1c = x1a + n2;
x4=w/2; bot y4=0;
x3=0.5[x2,x4]-3thin; y3=0.5[y2,y4];
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1--z2;         % top bar
draw z2..z3..z4;
pickup stylus;
filldraw z1a--z1b{up}..{right}z1c--cycle;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "7"

cmchar "SQRC digit 8";
beginchar("8", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
numeric alpha; alpha:=1;
pickup quill;
lft x1=alpha*pcshiftx+o; rt x3=w-alpha*pcshiftx-o; y1=y3=3/4h;
x2=x4=w/2; top y2=h+o; bot y4=-o;
x5=x2; y5=h/2+2o;
lft x11=pcshiftx; rt x13=w-pcshiftx; y11=y13=1/4h;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z5..z11..z4..z13..z5..cycle;
ductus:=false;
penlabels(1,2,3,4,5,11,13,100);
endchar; % end "8"

cmchar "SQRC digit 9";
beginchar("9", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
pickup quill;
lft x1=pcshiftx; bot y1=2thin;
x2=1/2w; bot y2= 0;
rt x3=w-pcshiftx; y3=1/2h;
x4=x2; top y4=h;
lft x5=pcshiftx; y5=3/4h;
rt x6=lft x3; top y6=y3;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2{right}..z3{up}..z4{left}..z5{down}..{pdir}z6;
ductus:=false;
penlabels(1,2,3,4,5,6,100);
endchar; % end "9"

cmchar "SQRC digit 0";
beginchar("0", 2u#+fig_width#, fig_height#, 0);
adjust_fit(0,0);
pcshiftx := u;
y100 = h/2; x100 = pcshiftx+y100;  % square center
pickup quill;
lft x1=pcshiftx; rt x3=w-pcshiftx; y1=y3=h/2;
x2=x4=w/2; top y2=h+o; bot y4=-o;
%%ductus:=true;
if ductus:
  pickup stylus;
fi
draw z1..z2..z3..z4..cycle;
ductus:=false;
penlabels(1,2,3,4,100);
endchar; % end "0"

